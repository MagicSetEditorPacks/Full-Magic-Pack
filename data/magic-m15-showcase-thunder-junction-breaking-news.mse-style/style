mse version:		2.1.2
game:				magic
short name:			Breaking News
full name:			Thunder Junction Showcase
icon:				card_sample.png
position hint:		095

version:			2024-10-01
depends on:
	package:			magic.mse-game
	version:			2014-06-25
depends on:
	package:			magic-mana-large.mse-symbol-font
	version:			2007-09-23
depends on:
	package:			magic-mana-small.mse-symbol-font
	version:			2007-09-23
depends on:
	package:			magic-modules.mse-include
	version:			2024-10-01
depends on:
	package:			magic-mainframe-extras.mse-include
	version:			2007-09-23

card width:			744
card height:		1039
card dpi:			297

### crown blanks by Card Conjurer, other blanks by GenevensiS
### code by GenevensiS

############################################################## Extra scripts

init script:
	swap_fonts_pt_default :=
	[
		name: {"Beleren Bold"},
		size: {31.0},
		color: { rgb(52, 46, 48) },
		vertical: {0},
		italic: {"Beleren Bold"}
	]
	
	card_stamp_offset_top_1 := { if stamp_shape() == "triangle" then 4 else 0 }
	swap_fonts_body_default :=
	[
		name: {"MPlantin"},
		size: {28.0},
		color: {"black"},
		vertical: {0},
		italic: {"MPlantin-Italic"}
	]
	
	typeline_offset_top_1 := { 29 }
	typeline_offset_width_1 := { -2 }
	typeline_offset_left_1 := { 1 }
	swap_fonts_type_default :=
	[
		name: {"RouletteCapsOpti"},
		size: {27.0},
		color: { rgb(52, 46, 48) },
		vertical: {0},
		italic: {""}
	]
	
	swap_fonts_name_default :=
	[
		name: {"RouletteCapsOpti"},
		size: {39.0},
		color: { rgb(52, 46, 48) },
		vertical: {0},
		italic: {""}
	]
	
	card_hybrid_2 :=
	{
		linear_blend(
			image1:					template(colors[0]),
			image2:					template(colors[1]),
									x1: 0.43,	y1: 0
									x2: 0.57,	y2: 0
		)
	}
	horizontal_card_hybrid :=
	[
		0:	horizontal_card_hybrid[0]
		1:	horizontal_card_hybrid[1]
		2:	card_hybrid_2
		3:	horizontal_card_hybrid[3]
		4:	horizontal_card_hybrid[4]
		5:	horizontal_card_hybrid[5]
		6:	horizontal_card_hybrid[6]
		7:	horizontal_card_hybrid[7]
	]
	card_hybrid :=
	[
		radial:		card_hybrid["radial"]
		horizontal:	horizontal_card_hybrid
		overlay:	card_hybrid["overlay"]
		vertical:	card_hybrid["vertical"]
	]
	
	template_prefix :=
	[
		card:				"card/",
		card2:				"card_crunched/",
		pt:					"pt/",
		crown:				"crown/",
		alias:				"alias/",
		stamp:				"stamp/",
		nonstamp:			"nonstamp/"
		identity:			"/magic-modules.mse-include/indicators/",
		identity2:			"/magic-modules.mse-include/indicators/",
	]
	template_suffix :=
	[
		card:				"card.png",
		card2:				"card.png",
		pt:					"pt.png",
		crown:				"crown.png",
		alias:				"alias.png",
		stamp:				"stamp.png",
		nonstamp:			"nonstamp.png"
		identity:			"identity.png",
		identity2:			"identity.png",
	]
	template := { template_prefix[type] + input + template_suffix[type] }
	land_template := { template_prefix[type] + (if input == "a" then "c" else input) + template_suffix[type] }
	
	card_background :=		{ color_background(input: card.card_color, type: "card",		base_hybrid: card_hybrid) }
	crunch_background :=	{ color_background(input: card.card_color, type: "card2",		base_hybrid: card_hybrid, blend_type:"card") }
	card_ptbox :=			{ color_background(input: card.card_color, type: "pt",			base_hybrid: card_hybrid) }
	card_crown :=			{ color_background(input: card.card_color, type: "crown",		base_hybrid: card_hybrid) }
	card_alias :=			{ color_background(input: card.card_color, type: "alias",		base_hybrid: card_hybrid) }
	card_stamp :=			{ color_background(input: card.card_color, type: "stamp",		base_hybrid: card_hybrid) }
	card_nonstamp :=		{ color_background(input: card.card_color, type: "nonstamp",	base_hybrid: card_hybrid) }
	
	pure_colors :=
	{
		sort_text(
			order: if card_is_wedge(input) then "(wbgur)" else "(wubrg)",
			(if chosen(card.card_color, choice:"white")	then "w") +
			(if chosen(card.card_color, choice:"blue")	then "u") +
			(if chosen(card.card_color, choice:"black")	then "b") +
			(if chosen(card.card_color, choice:"red")	then "r") +
			(if chosen(card.card_color, choice:"green")	then "g")
		)
	}
	pt_colors := { if styling.PT_color_scheme == "canon" then pt_colors_canon() else styling.PT_color_scheme }
	pt_colors_canon := { if is_hybrid_cost() then "land" else "gold" }
	
	is_promo := { styling.promo }
	
	is_unsorted := { styling.remove_from_autocount }

	
	is_crowned := { styling.legend_crown == "yes" or (styling.legend_crown == "if legendary" and lang_setting("is_legendary")(card.super_type)) }
	
	chop_top := { split := split_comma(styling.chop_text_box); if length(split) > 0 and split.0 != "" then clamp(split.0, maximum: 1500, minimum: -1500) else 0 }
	chop_bot := { split := split_comma(styling.chop_text_box); if length(split) > 1 and split.1 != "" then clamp(split.1, maximum: 1500, minimum: -1500) else 0 }
	
	popout_left := { split := split_comma(styling.popout_image_coordinates); if length(split) > 3 and split.0 != "" then clamp(split.0, maximum: 1500, minimum: -500) else 0 }
	popout_top := { split := split_comma(styling.popout_image_coordinates); if length(split) > 3 and split.1 != "" then clamp(split.1, maximum: 2000, minimum: -500) else 0 }
	popout_width := { split := split_comma(styling.popout_image_coordinates); if length(split) > 3 and split.2 != "" then clamp(split.2, maximum: 1500, minimum: 0) else 0 }
	popout_height := { split := split_comma(styling.popout_image_coordinates); if length(split) > 3 and split.3 != "" then clamp(split.3, maximum: 2000, minimum: 0) else 0 }

############################################################## Extra style options

styling field:
	type:			text
	name:			popout image coordinates
	description:	The coordinates for the popout image field, formatted as "left,top,width,height". Used to make the illustration jump in front of the frame.
styling field:
	type:			boolean
	name:			stretch image to whole card
	description:	Make the image span the whole card instead of just the visible part. Use this to help make popout effects.
	initial:		no

include file: /magic-modules.mse-include/texts/styling_fields

styling field:
	type:			choice
	name:			PT color scheme
	description:	Use the selected color scheme for the PT box on two color cards. Canon is "land" on hybrid cards, "gold" otherwise.
	choice:			canon
	choice:			gold
	choice:			hybrid
	choice:			colored
	choice:			land
	choice:			discolored
	initial:		canon
styling field:
	type:			choice
	name:			legend crown
	description:	Use the legend crown?
	choice:			yes
	choice:			if legendary
	choice:			no
	initial:		no

include file: /magic-modules.mse-include/stamps/styling_fields

include file: /magic-modules.mse-include/rarities/styling_fields

include file: /magic-modules.mse-include/watermarks/styling_fields

styling field:
	type:			package choice
	name:			overlay
	description:	Should there be an overlay applied, such as foil?
	match:			magic-overlay-*.mse-include
	required:		false
styling field:
	type:			boolean
	name:			colored border
	description:	Tint the border with the card's color?
	initial:		yes

include file: /magic-modules.mse-include/information/styling_fields

styling field:
	type:			package choice
	name:			casting cost mana symbols
	description:	Mana symbols used for casting cost, name, banner, fuse and transformation reminder.
	match:			magic-mana-*.mse-symbol-font
	initial:		magic-mana-thunder-junction-breaking-news.mse-symbol-font
styling field:
	type:			package choice
	name:			text box mana symbols
	description:	Mana symbols used inside rule text.
	match:			magic-mana-*.mse-symbol-font
	initial:		magic-mana-small.mse-symbol-font
styling field:
	type:			boolean
	name:			use ancestral mana symbols
	description:	Enables ancestral's modified generic mana.
	initial:		no
styling field:
	type:			boolean
	name:			use guild mana symbols
	description:	Use the Ravnica guild symbols instead of the official half/half circles for hybrid mana.
	initial:		no
styling field:
	type:			choice
	name:			tap symbol
	description:	What tap and untap symbols should be used on cards?
	choice:			modern
	choice:			old
	choice:			diagonal T
	initial:		modern
styling style:
	use ancestral mana symbols:
		choice images:
			yes:				/magic-modules.mse-include/symbol-fonts/generic_ancestral.png
			no:					/magic-modules.mse-include/symbol-fonts/generic_normal.png
	use guild mana symbols:
		choice images:
			yes:				/magic-modules.mse-include/symbol-fonts/hybrid_guild.png
			no:					/magic-modules.mse-include/symbol-fonts/hybrid_normal.png
	tap symbol:
		render style:	both
		choice images:
			modern:				/magic-modules.mse-include/symbol-fonts/tap_new.png
			old:				/magic-modules.mse-include/symbol-fonts/tap_old.png
			diagonal T:			/magic-modules.mse-include/symbol-fonts/tap_older.png

include file: /magic-modules.mse-include/fonts/styling_fields

############################################################## Card fields

include file: /magic-modules.mse-include/information/card_fields

include file: /magic-modules.mse-include/stamps/card_fields

include file: /magic-modules.mse-include/corners/card_fields

include file: /magic-modules.mse-include/watermarks/card_fields

include file: /magic-modules.mse-include/separators/card_fields

include file: /magic-modules.mse-include/typelines/card_fields

card style:
	############################# Background stuff
	border color:
		left:				0
		top:				0
		width:				{ stylesheet.card_width }
		height:				{ stylesheet.card_height }
		z index:			0
	card color:
		left:				0
		top:				0
		width:				{ stylesheet.card_width }
		height:				{ stylesheet.card_height }
		z index:			210
		popup style:		in place
		render style:		image
		image:				{ if card_style.casting_cost.content_width > 180 then crunch_background() else card_background() }
		mask:				{ if is_stamped() then "card_stamp_mask.png" else "card_mask.png" }
	############################# Name line
	name:
		left:				92
		top:				{ 92 + name_font_vertical() }
		right:				652
		height:				70
		z index:			1000
		alignment:			middle center shrink-overflow
		font:
			name:				{ name_font() }
			italic name:		{ name_font_italic() }
			size:				{ name_font_size() }
			scale down to:		8
			color:				{ name_font_color() }
	casting cost:
		right:				{ if card_style.casting_cost.content_width > 165 then 720 else 702 }
		top:				29
		width:				{ max(70, card_style.casting_cost.content_width + 12) }
		height:				54
		z index:			1010
		alignment:			middle right
		always symbol:		true
		font:
			name:				MPlantin
			size:				29
			color:				black
		symbol font:
			name:				{ styling.casting_cost_mana_symbols }
			size:				29
	############################# Image
	image:
		left:				{ if styling.stretch_image_to_whole_card then 0							else 38 }
		top:				{ if styling.stretch_image_to_whole_card then 0							else 177 }
		width:				{ if styling.stretch_image_to_whole_card then stylesheet.card_width		else 669 }
		height:				{ if styling.stretch_image_to_whole_card then stylesheet.card_height	else 421 }
		z index:			100
		mask:				{ if styling.stretch_image_to_whole_card then "image_mask_full.png" else "" }
	mainframe image:
		left:				{ popout_left() }
		top:				{ popout_top() }
		width:				{ popout_width() }
		height:				{ popout_height() }
		z index:			1010
	############################# Text box
	text:
		left:				65
		top:				{ 682 + chop_top() + (if is_stamped() then 3 else 0) + body_font_vertical() }
		right:				679
		bottom:				{ 947 - chop_bot() - if is_stamped() then 12 else 0 }
		z index:			900
		line height hard:	{ if is_modal(card.rule_text) then 0.9 else 1.2 }
		line height line:	1.5
		line height soft:	0.9
		line height hard max:	{ if is_modal(card.rule_text) then 1.0 else 1.3 }
		line height line max:	1.6
		alignment:
			script:
				if (styling.center_text == "short text only" and
				   not contains(match:"\n", card.rule_text) and
				   card.flavor_text == "<i-flavor></i-flavor>" and
				   card_style.text.content_lines <= 2) or
				   styling.center_text == "always"
				then "middle center"
				else "middle left"
		font:
			name:				{ body_font() }
			italic name:		{ body_font_italic() }
			size:				{ body_font_size() }
			scale down to:		12
			color:				{ body_font_color() }
		symbol font:
			name:				{ styling.text_box_mana_symbols }
			size:				{ body_font_size() }
			scale down to:		12
	############################# PT
	pt:
		left:				596
		top:				{ 933 + pt_font_vertical() }
		width:				93
		height:				45
		z index:			900
		alignment:			center middle shrink-overflow
		font:
			name:				{ pt_font() }
			size:				{ pt_font_size() }
			color:				{ pt_font_color() }
			separator color:	red

############################################################## Extra card fields

extra card field:
	type:				choice
	name:				pt box
	editable:			false
	save value:			false
	show statistics:	false
	choice:				pt box
extra card field:
	type:				choice
	name:				crown
	editable:			false
	save value:			false
	show statistics:	false
	choice:				crown
extra card field:
	type:				color
	name:				card colored border
	script:				rgb(65, 57, 60)
	editable:			false
	save value:			false
	show statistics:	false
extra card field:
	type:				choice
	name:				foil layer
	editable:			false
	save value:			false
	show statistics:	false
	choice:				foil layer

extra card style:
	pt box:
		left:				0
		top:				0
		width:				{ stylesheet.card_width }
		height:				{ stylesheet.card_height }
		z index:			810
		visible:			{ card.pt != "" }
		render style:		image
		image:
			script:
				pure := pure_colors()
				count := length(pure)
				color := pt_colors()
				if color == "discolored" then template("n", type: "pt")
				else if color == "gold" and count > 1 then template("m", type: "pt")
				else if color == "land" and count > 1 then template("c", type: "pt")
				else if color == "hybrid" and count == 2 then masked_blend(mask: "special_blend_pt.png", dark: template(pure.0, type: "pt"), light: template(pure.1, type: "pt"))
				else card_ptbox()
	crown:
		left:				0
		top:				0
		width:				{ stylesheet.card_width }
		height:				{ stylesheet.card_height }
		z index:			310
		visible:			{ is_crowned() }
		render style:		image
		image:				{ card_crown() }
	card colored border:
		left:				0
		top:				0
		width:				{ stylesheet.card_width }
		height:				{ stylesheet.card_height }
		z index:			1
		visible:			{ styling.colored_border }
	foil layer:
		left:				0
		top:				0
		width:				{ stylesheet.card_width }
		height:				{ stylesheet.card_height }
		z index:			1050
		visible:			{ styling.overlay != "none" and styling.overlay != "" }
		render style:		image
		image:				{ if styling.overlay == "" then "" else styling.overlay + "/overlay.png" }
