mse version: 2.0.0
game: magic
short name: M15 Adventures
full name: After M15 Adventures
icon: card-sample.png
installer group: magic/m15 style/split cards
position hint: 043

version: 2015-12-20
depends on:
	package: magic.mse-game
	version: 2014-06-25
depends on:
	package: magic-default-image.mse-include
	version: 2007-09-23
depends on:
	package: magic-watermarks.mse-include
	version: 2007-09-23
depends on:
	package: magic-identity-new.mse-include
	version: 2012-01-22
depends on:
	package: magic-mana-large.mse-symbol-font
	version: 2007-09-23
depends on:
	package: magic-mana-small.mse-symbol-font
	version: 2007-09-23
depends on:
	package: magic-mainframe-extras.mse-include
	version: 2018-01-01

card width: 375
card height: 523
card dpi: 150
###### Credits
###### Standard blanks by kebelqwa
###### Spotlight blanks by Femme Fatale
###### Cleanup and coding by cajun
###### Reverse Adventures by BA Start
############################################################## Extra scripts
init script:
	# Load scripts for image box
	include file: /magic-default-image.mse-include/scripts
	
	# Should hybrids have a grey name?
	mask_hybrid_with_land := { styling.grey_hybrid_name }
	
	#Should multicolor lands with basic land types have a colored name?
	mask_multi_land_with_color := { styling.colored_multicolor_land_name }
	
	template_prefix := [card: "" card2: "" card3: "reverse/" card4: "reverse/" crown: "legend/" nyx: "nyx/" spotpt: "spotlight/" spot: "spotlight/" page: "spotlight/" pt: "" stamp: "" snowtexture:"snow/" alias:"alias/" identity: "/magic-identity-new.mse-include/" identity2: "/magic-identity-new.mse-include/"]
	template_suffix := [card: "card.jpg" card2: "card2.png" card3: "card.jpg" card4: "card2.png" crown: "crown.png" nyx: "nyx.png" spotpt: "pt.png" spot: "card.png" page: "card2.png" pt: "pt.png", stamp: "stamp.jpg" alias:"alias.png" snowtexture:"snow.png" identity: "identity.png" identity2: "identity.png"]
	template      := { template_prefix[type] + input + template_suffix[type] }
	land_template := { template_prefix[type] + input + (if input == "a" or type == "nyx" then "" else "l") + template_suffix[type] }
	# Use land templates for previews because they show more contrast
	hybrid_previews := "land,hybrid"
	card_background2 := { color_background(type:"card2", base_hybrid:card_hybrid) }
	card_background3 := { color_background(type:"card3", base_hybrid:card_hybrid) }
	card_background4 := { color_background(type:"card4", base_hybrid:card_hybrid) }
	spot_background := { color_background(type:"spot", base_hybrid:card_hybrid) }
	page_background := { color_background(type:"page", base_hybrid:card_hybrid) }
	alias_bar := { color_background(type:"alias",     base_hybrid:card_hybrid) }
	spot_pt := { color_background(type:"spotpt", base_hybrid:card_hybrid) }
	crown_background := { color_background(type:"crown", base_hybrid: card_hybrid)}
	nyx_background := { color_background(type:"nyx",     base_hybrid:card_hybrid) }
	snow_background := { color_background(type:"snowtexture",     base_hybrid:card_hybrid) }
	# Use the normal tap symbol
	mana_t := {
		if      styling.tap_symbol == "old"        then "old"
		else if styling.tap_symbol == "diagonal T" then "older"
		else                                            "new"
	}
	
	# Use guild mana symbols?
	guild_mana := { styling.use_guild_mana_symbols }
	
	# Is the card a promo card?
	is_promo := { styling.promo }
	has_identity := { styling.color_indicator_dot }
	has_identity2 := { styling.color_indicator_dot_2 }
	is_legend := {not is_spot() and ((set.auto_legends and contains(card.super_type, match:"Legendary"))) }
	is_spot := {contains(styling.frames, match:"Spotlight")}
	is_reversed := { not is_spot() and contains(styling.frames, match:"Reversed")}
	is_vehicle := {not is_spot() and (contains(styling.frames, match:"Vehicle") or (contains(styling.auto_frames, match:"Vehicle") and contains(card.sub_type, match:"Vehicle")))}
	is_nyx := {not is_spot() and (contains(styling.frames, match:"Nyx") or (contains(styling.auto_frames, match:"Nyx") and contains(card.type, match:"Enchantment "))) }
	is_snow := {not is_spot() and (contains(styling.frames, match:"Snow") or (contains(styling.auto_frames, match:"Snow") and contains(card.type, match:"Snow"))) }
	##### flavor bar stuff
	chop_correction := { chop_bot() }
	top_of_textbox := {327-(-chop_top())}
	bottom_of_textbox := {(if is_spot() then 464 else 481)-chop_bot()}

	top_of_textbox2 := {375-(-back_chop_top())}
	bottom_of_textbox2 := {(if is_spot() then 464 else 481)-back_chop_bot()}
	
	chop_top := {to_number(pull_comma_array(styling.chop_main, cell:0, end:false))}
	chop_bot := {to_number(pull_comma_array(styling.chop_main, cell:1, end:false))}
	back_chop_top := {to_number(pull_comma_array(styling.chop_adventure, cell:0, end:false))}
	back_chop_bot := {to_number(pull_comma_array(styling.chop_adventure, cell:1, end:false))}
	##### backface bar
	flavor_text2 := {remove_tags(card.flavor_text_2) + "\n"}
	chop_correction2 := { 0 }
	is_unsorted := {styling.remove_from_autocount}
	
	shrink_type := {if styling.shrink_typeline_text != "" then to_int((if comma_count(styling.shrink_typeline_text) == "" then styling.shrink_typeline_text else split_text(match:",", styling.shrink_typeline_text).0)) else 0 }
	shrink_name := {if styling.shrink_name_text != "" then to_int((if comma_count(styling.shrink_name_text) == "" then styling.shrink_name_text else split_text(match:",", styling.shrink_name_text).0)) else 0 }
	shrink_type2 := {if styling.shrink_typeline_text != "" then to_int((if comma_count(styling.shrink_typeline_text) != "" then split_text(match:",", styling.shrink_typeline_text).1)) else 0 }
	shrink_name2 := {if styling.shrink_name_text != "" then to_int((if comma_count(styling.shrink_name_text) != "" then split_text(match:",", styling.shrink_name_text).1)) else 0 }
	use_main_rarity := { contains(set.mainframe_rarity_name, match: ".png")}
	un_png := replace@(match:".png", replace: "")
	use_evobar := {contains(styling.other_options, match:"pokemon evobar")}
	is_skinned := {contains(styling.other_options, match:"godzilla style alias")}

	card_shape := {"adventure"}
############################################################## Set info fields
set info style:
	symbol:
		variation:
			name: invertedcommon
			border radius: 0.10
			fill type: solid
			fill color:   rgb(255,255,255)
			border color: rgb(0,0,0)
############################################################## Extra style options
styling field:
	type: text
	name: chop main
	description: Format as "X,Y". Shrinks main textbox X pixels from the top and Y from bottom.
styling field:
	type: text
	name: chop adventure
	description: Format as "X,Y". Shrinks adventure textbox X pixels from the top and Y from bottom.
styling field:
	type: text
	name: shrink name text
	description: Format as "X,Y". Shrinks main name X pixels and adventure name Y pixels.
styling field:
	type: text
	name: shrink typeline text
	description: Format as "X,Y". Shrinks main type X pixels and adventure type Y pixels.
styling field:
	type: multiple choice
	name: frames
	description: Frames to use. Higher choices may override lower choices.
	choice: Spotlight
	choice: Reversed
	choice: Nyx
	choice: Vehicle
	choice: Snow
styling field:
	type: multiple choice
	name: auto frames
	description: Automatically applies frames for multi-typed enchantments or Vehicles on the main card.
	choice: Nyx
	choice: Vehicle
	choice: Snow
styling field:
	type: multiple choice
	name: other options
	description: Other options for the frame
	choice: godzilla style alias
	choice: pokemon evobar
	choice: prerelease stamp
styling field:
	type: boolean
	name: remove from autocount
	description: Removes the automatic card number for specific cards, to allow for overcounted cards like in Planeswalker Decks.
	initial: no
styling field:
	type: boolean
	name: color indicator dot
	description: Use the color indicator dot
	initial: no
styling field:
	type: boolean
	name: color indicator dot 2
	description: Use the color indicator dot for the adventure half
	initial: no
styling field:
	type: boolean
	name: use holofoil stamps
	description: Use holofoil stamps on rares and mythics
styling field:
	type: boolean
	name: grey hybrid name
	description: Use a grey background for the name and type line on hybrid cards. This is done on real cards.
styling field:
	type: boolean
	name: colored multicolor land name
	description: Use a colored background for the name and type on multicolor land cards with a basic land type. This is done on real cards.
styling field:
	type: boolean
	name: use guild mana symbols
	description: Use the Ravnica guild symbols instead of the official half/half circles for hybrid mana.
	initial: no
styling field:
	type: choice
	name: tap symbol
	description: What tap and untap symbols should be used on cards?
	initial: modern
	choice: modern
	choice: old
	choice: diagonal T
styling field:
	type: package choice
	name: text box mana symbols
	match: magic-mana-*.mse-symbol-font
	initial: magic-mana-small.mse-symbol-font
styling field:
	type: choice
	name: center text
	description: When to center text (short text only means only on one-line cards with no flavor text)
	choice: always
	choice: short text only
	choice: never
	initial: never
styling field:
	type: boolean
	name: promo
	description: Is this card a promo card, with the "P" rarity?
	initial: no
styling field:
	type: boolean
	name: inverted common symbol
	description: Should the common rarity symbol be inverted, like in Coldsnap?
	initial: no
styling field:
	type: package choice
	name: overlay
	description: Should there be an overlay applied, such as foil?
	match: magic-overlay-*.mse-include
	required: false
styling style:
	use guild mana symbols:
		choice images:
			yes: /magic-mana-small.mse-symbol-font/mana_guild_rg.png
			no:  /magic-mana-small.mse-symbol-font/mana_rg.png
	tap symbol:
		render style: both
		choice images:
			modern:     /magic-mana-large.mse-symbol-font/mana_t.png
			old:        /magic-mana-large.mse-symbol-font/mana_t_old.png
			diagonal T: /magic-mana-large.mse-symbol-font/mana_t_older.png
	center text:
		render style: both
		choice images:
			always:          /magic.mse-game/icons/center-all-text.png
			short text only: /magic.mse-game/icons/center-short-text.png
			never:           /magic.mse-game/icons/center-no-text.png
	inverted common symbol:
		choice images:
			no:  { symbol_variation(symbol: set.symbol, variation: "common") }
			yes: { symbol_variation(symbol: set.symbol, variation: "invertedcommon") }
	frames:
		render style: checklist
		direction: vertical
	auto frames:
		render style: checklist
		direction: vertical
	other options:
		render style: checklist
		direction: vertical
############################################################## Card fields
card style:
	############################# Background stuff
	border color:
		left: 0
		top : 0
		width:	375
		height:	523
		radius: 18
		left width:   17
		right width:  17
		top width:    17
		bottom width: 18
		z index: 4
		mask: { (if is_spot() then "spot_" else "") + (if is_legend() then "crown" else "border") + "_mask" + (if not is_spot() and card.border_color == rgb(200,200,200) then "_silver") + ".png"}
	card color:
		left:	0
		top:	0
		width:	375
		height:	523
		z index: 0
		render style: image
		popup style: in place
		image: { if is_spot() then spot_background() else if is_reversed() then card_background3() else card_background() }
	card color 2:
		left:	{if is_spot() then 21 else if is_reversed() then 187 else 20}
		top:	{if is_spot() then 319 else 327}
		width:	{if is_spot() then 170 else 169}
		height:	{if is_spot() then 170 else 155}
		z index: 0
		render style: image
		popup style: in place
		image: { if is_spot() then page_background() else if is_reversed() then card_background4() else card_background2() }
	############################# Name line
	name:
		left: { (if is_spot() then 22 else 0) + (if card.card_symbol=="none" then 32 else 50) }
		top: 30
		right:  { 341 - card_style.casting_cost.content_width }
		height: { 23 - 0.5*shrink_name() }
		alignment: bottom shrink-overflow
		padding bottom: 0
		z index: 1
		font:
			name: Beleren Bold
			size: {16 - shrink_name() }
			color: black
	name 2:
		left: { if is_reversed() then 197 else 32}
		top: { 328 + (if is_spot() then 2 else 0)}
		right: { (if is_reversed() then 28 else 178) - card_style.casting_cost_2.content_width }
		height: { 20 - shrink_name2() }
		alignment: bottom shrink-overflow
		z index: 2
		font:
			name: Beleren Bold
			size: {12 - shrink_name2() }
			color: white
	alias: 
		left: {if is_skinned() then 45 else 33}
		top: 53
		width: {if is_skinned() then 285 else if use_evobar() then max(100, card_style.alias.content_width) else 0}
		height: 20
		alignment: { (if is_skinned() then "center") + "bottom shrink-overflow"}
		padding bottom: 0
		z index: 3
		font:
			name: {if use_evobar() then "Matrix" else "MPlantin-Italic"}
			size: {if use_evobar() then 10 else 9}
			style: {if use_evobar() then "italic" else "bold"}
			weight: {if use_evobar() then "italic" else "bold"}
			color: {if use_evobar() then "black" else "white"}
	casting cost:
		right: 346
		top: 29
		width: { max(30, card_style.casting_cost.content_width) + 5 }
		height: 23
		alignment: middle right
		font:
			name: MPlantin
			size: 15
		symbol font:
			name: magic-mana-large
			size: 15
			alignment: middle right
		always symbol: true
		z index: 2
		padding top: 0
	casting cost 2:
		right: {if is_reversed() then 345 else 180}
		top: { if is_spot() then 330 else 328}
		width: { max(30, card_style.casting_cost_2.content_width) + 5 }
		height: 23
		alignment: middle right
		symbol font:
			name: magic-mana-large
			size: 12
			alignment: middle right
		always symbol: true
		z index: 2
		padding top: 0
	card symbol:
		left: {if card.card_symbol=="none" then 20 else 30}
		top: 29
		height: 20
		width: 14
		z index: 1
		render style: image
		choice images:
			tombstone: tombstone.png
	############################# Image
	image:
		left: {if is_spot() then 10 else 29}
		top: {if is_spot() then 13 else 59}
		width: { if is_spot() then 354 else 316}
		height:	{ if is_spot() then 298 else 231}
		z index: 1
		default: {default_image(card.card_color)}
		mask: { if is_spot() then "imagemask.png" else ""}
	############################# Card type
	indicator:
		left: { if is_spot() then 80 else 31 }
		top: { if is_spot() then 303 else 300 }
		width: 17
		height: 17
		z index: 1
		render style: image
		visible: { has_identity() }
		image: { card_identity() }
	type:
		left: { (if is_spot() then 50 else 0) + (if has_identity() then (if is_spot() then 50 else 52) else 32) }
		top: { shrink_type() + if is_spot() then 298 else 296}
		width: { (if has_identity() then "290" else "310") - max(22,card_style.rarity.content_width) - (if is_spot() then 73 else 0) }
		height: { 20 - shrink_type() }
		alignment: { if is_spot() then "top center shrink-overflow" else "top shrink-overflow"}
		z index: 1
		padding top: 2
		font:
			name: Beleren Bold
			size: { 13 - shrink_type() }
			color: black
			separator color: red
	indicator 2:
		left: {(if is_reversed() then 165 else 0) + (if has_identity2() then 30 else 0)}
		top: {355 + (if is_spot() then 2 else 0) }
		width: 15
		height: 15
		z index: 2
		render style: image
		visible: {has_identity2()}
		image: {card_identity_2()}
	type 2:
		left: {(if is_reversed() then 197 else 32) + (if has_identity2() then 20 else 0)}
		top: {shrink_type2() + 352 + (if is_spot() then 1 else 0)}
		width: {155 - (if has_identity2() then 20 else 0)}
		height: { 20 - shrink_type2() }
		alignment: top shrink-overflow
		z index: 1
		padding top: 2
		font:
			name: Beleren Bold
			size: { 11 - shrink_type2() }
			color: white
			separator color: red
	rarity:
		right: { if is_spot() then 342 else 344}
		top: { if is_spot() then 299 else 297}
		width: { if is_spot() then 22 else 44}
		height: 22
		z index: 2
		render style: image
		alignment: { if is_spot() then "middle center" else "middle right"}
		choice images:
			# Images based on the set symbol
			basic land:
				script:
					if use_main_rarity() then "/magic-mainframe-extras.mse-include/" + un_png(set.mainframe_rarity_name) + "c.png"
					else if styling.inverted_common_symbol then symbol_variation(symbol: set.symbol, variation: "invertedcommon")
					else symbol_variation(symbol: set.symbol, variation: "common")
			common:
				script:
					if use_main_rarity() then "/magic-mainframe-extras.mse-include/" + un_png(set.mainframe_rarity_name) + "c.png"
					else if styling.inverted_common_symbol then symbol_variation(symbol: set.symbol, variation: "invertedcommon")
					else symbol_variation(symbol: set.symbol, variation: "common")
			uncommon:
				script:
					if use_main_rarity() then "/magic-mainframe-extras.mse-include/" + un_png(set.mainframe_rarity_name) + "u.png"
					else symbol_variation(symbol: set.symbol, variation: "uncommon")
			rare:
				script:
					if use_main_rarity() then "/magic-mainframe-extras.mse-include/" + un_png(set.mainframe_rarity_name) + "r.png"
					else symbol_variation(symbol: set.symbol, variation: "rare")
			mythic rare:
				script:
					if use_main_rarity() then "/magic-mainframe-extras.mse-include/" + un_png(set.mainframe_rarity_name) + "m.png"
					else symbol_variation(symbol: set.symbol, variation: "mythic rare")
			special:
				script:
					if use_main_rarity() then "/magic-mainframe-extras.mse-include/" + un_png(set.mainframe_rarity_name) + "s.png"
					else symbol_variation(symbol: set.symbol, variation: "special")
			masterpiece:
				script:
					if use_main_rarity() then "/magic-mainframe-extras.mse-include/" + un_png(set.mainframe_rarity_name) + "mp.png"
					else symbol_variation(symbol: set.masterpiece_symbol, variation: "mythic rare")
	############################# Text box
	text:
		left: {if is_reversed() then 27 else 200}
		top: { 328 + to_int(chop_top()) }
		width: {if is_reversed() then 157 else 143}
		bottom: { (if is_spot() then 464 else 481) - to_int(chop_bot()) }
		font:
			name: MPlantin
			italic name: MPlantin-Italic
			size: 13
			scale down to: 6
			color: black
		symbol font:
			name: { styling.text_box_mana_symbols }
			size: 13
		alignment:
			script:
				if (styling.center_text == "short text only" and
				   not contains(match:"\n", card.rule_text) and
				   card.flavor_text == "<i-flavor></i-flavor>" and
				   card_style.text.content_lines <= 2) or
				   styling.center_text == "always"
				then "middle center"
				else "middle left"
		z index: 2
		padding left:   6
		padding right:  4
		line height hard: {if is_modal(card.rule_text) then 0.9 else 1.2 }
		line height line: 1.5
		line height soft: 0.9
		line height hard max: {if is_modal(card.rule_text) then 1.0 else 1.3 }
		line height line max: 1.6
	text 2:
		left: {if is_reversed() then 190 else 29}
		top: { 375 + to_int(back_chop_top()) }
		width: 157
		bottom: { (if is_spot() then 464 else 481) - to_int(back_chop_bot()) }
		font:
			name: MPlantin
			italic name: MPlantin-Italic
			size: 14
			scale down to: 6
			color: black
		symbol font:
			name: { styling.text_box_mana_symbols }
			size: 14
		alignment:
			script:
				if (styling.center_text == "short text only" and
				   not contains(match:"\n", card.rule_text) and
				   card.flavor_text == "<i-flavor></i-flavor>" and
				   card_style.text.content_lines <= 2) or
				   styling.center_text == "always"
				then "middle center"
				else "middle left"
		z index: 2
		padding left:   6
		padding right:  4
		line height hard: 1.2
		line height line: 1.5
		line height soft: 0.9
		line height hard max: 1.3
		line height line max: 1.6
	watermark:
		left: { if is_reversed() then (if is_spot() then 30 else 20) else if is_spot() then 195 else 188}
		top: { if is_spot() then 340 else 328}
		width: { if is_spot() then 150 else 168}
		height: { if is_spot() then 130 else 153 }
		z index: 1
		render style: image
		popup style: in place
		alignment: middle center
		include file: /magic-watermarks.mse-include/watermarks
	watermark 2:
		left: { if is_reversed() then (if is_spot() then 195 else 188) else if is_spot() then 30 else 20}
		top: { if is_spot() then 380 else 375}
		width: { if is_spot() then 154 else 167}
		height: { if is_spot() then 88 else 108}
		z index: 1
		render style: image
		popup style: in place
		alignment: middle center
		include file: /magic-watermarks.mse-include/watermarks
	############################# PT
	pt:
		z index: 2
		left: { if is_spot() then 293 else 286}
		top: {if is_spot() then 466 else 469}
		width: 60
		height:	28
		alignment: center middle shrink-overflow
		font:
			name: Beleren Bold
			size: 16
			color: {if is_vehicle() then "white" else "black"}
			separator color: red
	############################# Card sorting / numbering
	set code:
		left: 24
		top: 498
		width: 40
		height: 10
		z index: 1
		font:
			name: Relay-Medium
			size: 7
			color: white
			weight: bold
	############################# Copyright stuff
	illustrator:
		left: { 44 + card_style.set_code.content_width }
		top: 497.5
		width: 200
		height: 10
		z index: 1
		font:
			name: Beleren Small Caps Bold
			size: 7.25
			color: white
	copyright:
		right: 350
		top: { if card.pt != "" then 500 else 488 }
		width: {if set.automatic_copyright then 0 else 140}
		height: 10
		z index: 2
		alignment: middle right shrink-overflow
		font:
			name: Matrix
			size: 7
			color: white
			weight: bold
############################################################## Extra card fields
extra card field:
	type: text
	name: card code
	save value: false
	script:
		if set.automatic_card_numbers and not styling.remove_from_autocount then
			forward_editor(prefix: card_number_m15() + "/" + card_count_m15() + " " + rarity_code() + " ", field: card.card_code_text)
		else
			combined_editor(field1: card.custom_card_number, separator: " " + rarity_code() + " ", field2: card.card_code_text)
extra card field:
	type: choice
	name: artist arrow
	editable: false
	save value: false
	choice: white
extra card field:
	type: choice
	name: alias bar
	script: card.card_color
	save value: false
	editable: false
extra card field:
	type: choice
	name: pt box
	script: card.card_color
	editable: false
	save value: false
extra card field:
	type: choice
	name: stamp
	script: card.card_color
	editable: false
	save value: false
extra card field:
	type: choice
	name: foil stamp
	choice: stamp
	save value: false
	editable: false
extra card field:
	type: choice
	name: foil layer
	choice: foil
	save value: false
	editable: false
extra card field:
	type: choice
	name: flavor bar
	choice: bar
	editable: false
	save value: false
	show statistics: false
extra card field:
	type: choice
	name: flavor bar 2
	choice: bar
	editable: false
	save value: false
	show statistics: false
extra card field:
	type: choice
	name: crown
	choice: crown
	editable: false
	save value: false
	show statistics: false
	script: card.card_color
extra card field:
	type: choice
	name: snow overlay
	choice: overlay
	editable: false
	save value: false
	show statistics: false
	script: card.card_color
extra card field:
	type: choice
	name: vehicle overlay
	choice: overlay
	editable: false
	save value: false
	show statistics: false
extra card field:
	type: choice
	name: nyx overlay
	choice: overlay
	editable: false
	save value: false
	show statistics: false
	script: card.card_color
extra card field:
	type: choice
	name: evobar tip
	choice: evo
	save value: false
	editable: false
extra card field:
	type: choice
	name: evobar start
	choice: evo
	save value: false
	editable: false
extra card field:
	type: choice
	name: evobar
	choice: evo
	save value: false
	editable: false
extra card field:
	type: text
	name: auto copyright
	script: set.copyright
	save value: false
	editable: false
	show statistics: false
extra card field:
	type: text
	name: vorthos box
	save value: true
	editable: true
	description: Where they put mtgstory.com on story spotlights.
extra card field:
	type: text
	name: promo label
	save value: true
	description: The golden promo label.
extra card style:
	card code:
		left: 24
		top: 488
		width: 120
		height: 10
		z index: 1
		font:
			name: Relay-Medium
			size: 7
			color: white
			weight: bold
	promo label:
		left: {if is_spot() then 65 else 40}
		top: 262
		width: {if chosen(styling.other_options, choice:"prerelease stamp") then (if is_spot() then 245 else 295) else 0}
		height: 29
		alignment: middle right
		z index: 4
		font:
			name: ModMatrix
			size: 14
			color: rgb(223,169,41)
	artist arrow:
		left: { 28 + card_style.set_code.content_width }
		top: 500
		width: 12
		height: 7
		z index: 1
		render style: image
		image: artist_arrow.png
	pt box:
		left: {if is_spot() then 290 else 273}
		top: {if is_spot() then 460 else 466}
		width: {if is_spot() then 68 else 81}
		height:	{ if is_spot() then 38 else 42}
		z index: 1
		visible: { card.pt != "" }
		render style: image
		image: { if is_spot() then spot_pt() else if is_vehicle() then "vpt.png" else card_ptbox() }
	stamp:
		left: 165
		top: 472
		width: 46
		height: 26
		z index: 1
		render style: image
		image: { card_stamp() }
		mask: { if is_spot() then "spotmask.jpg" else "stampmask.jpg"}
		visible: {is_rare() and styling.use_holofoil_stamps}
	foil stamp:
		left: 165
		top: 472
		width: 46
		height: 26
		z index: 1
		visible: { is_rare() and styling.use_holofoil_stamps }
		render style: image
		image: foil_stamp.png
	foil layer:
		left: 0
		top : 0
		width: 375
		height: 523
		z index: 3
		render style: image
		image: {if styling.overlay == "" then nil else styling.overlay + "/overlay.png"}
		mask: { if is_rare() then "foil_mask_rare.png" else "foil_mask.png" }
	flavor bar:
		left: { if is_reversed() then 10 else 175}
		top: { bar_equation() }
		width: 188
		height: 1
		z index: 1
		render style: image
		image: bar.png
		visible: { card.rule_text != "" and remove_tags(card.flavor_text) != "" and set.use_flavor_bar }
	flavor bar 2:
		left: {if is_reversed() then 175 else 10}
		top: { bar_equation2() }
		width: 188
		height: 1
		z index: 1
		render style: image
		image: bar.png
		visible: { card.rule_text_2 != "" and remove_tags(card.flavor_text_2) != "" and set.use_flavor_bar }
	crown:
		left: 0
		top: 0
		width: 375
		height: 523
		render style: image
		image: {crown_background()}
		z index: 3
		visible: {is_legend() and not is_spot()}
	vehicle overlay:
		left: 0
		top: 0
		width: 375
		height: 523
		render style: image
		image: {"voverlay.png" }
		z index: 0
		visible: {is_vehicle()}
	nyx overlay:
		left: 0
		top: 0
		width: 375
		height: 523
		render style: image
		image: {nyx_background()}
		z index: 0
		visible: {is_nyx()}
		mask: {"nyx/" + (if is_vehicle() or is_snow() then "v_") + "mask.png"}
	snow overlay:
		left: 0
		top: 0
		width: 375
		height: 523
		render style: image
		image: {snow_background()}
		z index: 0
		visible: {is_snow()}
		mask: {"snow/snow_mask.png"}
	evobar:
		left: 27
		top: 57
		width: {card_style.alias.content_width}
		height: 27
		z index: 2
		render style: image
		image: evobar.png
		visible: {use_evobar()}
	evobar tip:
		left: {26.6 + card_style.alias.content_width}
		top: 57
		width: 36
		height: 27
		z index: 2
		render style: image
		image: evobar_tip.png
		visible: {use_evobar()}
	auto copyright:
		right: 350
		top: { if card.pt != "" then 500 else 488 }
		width: {if set.automatic_copyright then 140 else 0}
		height: 10
		z index: 4
		alignment: middle right shrink-overflow
		font:
			name: Matrix
			size: 7
			color: white
			weight: bold
	alias bar:
		left: 38
		top: 57
		width: 298
		height: 21
		z index: 2
		render style: image
		image: {alias_bar()}
		visible: {is_skinned() and not use_evobar()}
	vorthos box:
		right: { if card.pt != "" then 275 else 350 }
		top: 488
		left: 210
		height: 10
		z index: 3
		alignment: middle right
		font:
			name: Relay-Medium
			size: 7
			color: white
			weight: bold

