mse version: 2.0.0
game: magic
short name: SevenHalf
full name: 7.5th Edition Frames
installer group: magic/seven and a half style
icon: card-sample.png
position hint: 221

version: 2014-02-22
depends on: magic.mse-game                   2008-06-02
depends on: magic-default-image.mse-include  2007-09-23
depends on: magic-watermarks.mse-include     2007-09-23
depends on: magic-mana-large.mse-symbol-font 2007-09-23
depends on: magic-mana-small.mse-symbol-font 2007-09-23
depends on: magic-future-common.mse-include  2007-05-21

card width: 375
card height: 523
card dpi: 150

############################################################## Extra scripts

init script:
	# Load scripts for image box
	include file: /magic-default-image.mse-include/scripts
	
	# Only white cards use a black font
	font_colors_white := { input != "w" }
	
	# Should hybrids have a grey name?
	mask_hybrid_with_land := { styling.grey_hybrid_name }
	
	#Should multicolor lands with basic land types have a colored name?
	mask_multi_land_with_color := { styling.colored_multicolor_land_name }
	
	template_suffix := [card: "card.jpg", pt: "pt.png"]
	template      := { input + template_suffix[type] }
	land_template := { (if input == "a" then "c" else input) + land_template_suffix[type] }
	land_template_suffix := [card: "lcard.jpg", pt: "pt.png"]
	# Use land templates for previews because they show more contrast
	hybrid_previews := "land,hybrid"
	
	# Use the normal tap symbol
	mana_t := {
		if      styling.tap_symbol == "old"        then "old"
		else if styling.tap_symbol == "diagonal T" then "older"
		else                                            "new"
	}
	
	# Use guild mana symbols?
	guild_mana := { styling.use_guild_mana_symbols }
	
	# Alpha style lands?
	color_combination_std := color_combination
	color_combination := {
		if type == "card" and land and color_count == 2 and shape != "overlay" and styling.alpha_style_blending then
			masked_blend(
				mask:  "alpha_blend.png",
				dark:  land_template(colors[0]),
				light: land_template(colors[1]),
			)
		else color_combination_std()
	}

############################################################## Set info fields
set info style:
	symbol:
		variation:
			name: olduncommon
			border radius: 0.10
			fill type: linear gradient
			fill color 1:   rgb(224,224,224)
			fill color 2:   rgb(84, 84, 84)
			border color 1: rgb(255,255,255)
			border color 2: rgb(255,255,255)
		variation:
			name: oldrare
			border radius: 0.10
			fill type: linear gradient
			fill color 1:   rgb(214,196,94)
			fill color 2:   rgb(95, 84, 40)
			border color 1: rgb(255,255,255)
			border color 2: rgb(255,255,255)
		variation:
			name: oldmyth
			border radius: 0.10
			fill type: linear gradient
			fill color 1:   rgb(255, 160, 40)
			fill color 2:   rgb(202, 49, 35)
			border color 1: rgb(255,255,255)
			border color 2: rgb(255,255,255)
		variation:
			name: invertedcommon
			border radius: 0.10
			fill type: solid
			fill color:   rgb(255,255,255)
			border color: rgb(0,0,0)
############################################################## Extra style options
styling field:
	type: choice
	name: cardname font
	description: Use Matrix or MagicMedieval for the cardname font?
	initial: Matrix
	choice: Matrix
	choice: MagicMedieval
styling field:
	type: boolean
	name: colored multicolor land name
	description: Use a colored background for the name and type on multicolor land cards with a basic land type.
styling field:
	type: boolean
	name: grey hybrid name
	description: Use a grey background for the name and type line on hybrid cards. This is done on real cards.
styling field:
	type: boolean
	name: use guild mana symbols
	description: Use the Ravnica guild symbols instead of the official half/half circles for hybrid mana.
	initial: no
styling field:
	type: choice
	name: tap symbol
	description: What tap symbol should be used on cards?
	initial: modern
	choice: modern
	choice: old
	choice: diagonal T

styling field:
	type: package choice
	name: text box mana symbols
	match: magic-mana-*.mse-symbol-font
	initial: magic-mana-small.mse-symbol-font
styling field:
	type: boolean
	name: old rarities
	description: Should non-common symbols have white borders, like before 8th Edition?
	initial: no
styling field:
	type: boolean
	name: inverted common symbol
	description: Should the common rarity symbol be inverted, like in Coldsnap?
	initial: no
styling field:
	type: choice
	name: image size
	description: What is the size of the image for this card?
	initial: standard
	choice: standard
	choice: extended
styling field:
	type: boolean
	name: center short text
	description: Center the text on cards with short rule text and no flavor text.
	initial: no
styling field:
	type: boolean
	name: alpha style blending
	description: Use Alpha-style dual blending
	initial: no
styling style:
	use guild mana symbols:
		choice images:
			yes: /magic-mana-small.mse-symbol-font/mana_guild_rg.png
			no:  /magic-mana-small.mse-symbol-font/mana_rg.png
	tap symbol:
		render style: both
		choice images:
			modern:     /magic-mana-large.mse-symbol-font/mana_t.png
			old:        /magic-mana-large.mse-symbol-font/mana_t_old.png
			diagonal T: /magic-mana-large.mse-symbol-font/mana_t_older.png
	center short text:
		render style: both
		choice images:
			always:          /magic.mse-game/icons/center-all-text.png
			short text only: /magic.mse-game/icons/center-short-text.png
			never:           /magic.mse-game/icons/center-no-text.png
	inverted common symbol:
		choice images:
			no:  { symbol_variation(symbol: set.symbol, variation: "common") }
			yes: { symbol_variation(symbol: set.symbol, variation: "invertedcommon") }
	image size:
		render style: both
		choice images:
			standard: /magic.mse-game/icons/small-image.png
			extended: /magic.mse-game/icons/large-image.png
############################################################## Card fields
card style:
	############################# Background stuff
	border color:
		left: 0
		top : 0
		width:	375
		height:	523
		radius: 18
		left width:   17
		right width:  17
		top width:    17
		bottom width: 18
		z index: -1
	card color:
		left:	0
		top:	0
		width:	375
		height:	523
		z index: -2
		render style: image
		popup style: in place
		image: { card_background() }
	############################# Name line
	name:
		left:   { if card.card_symbol=="none" then 32 else 51 }
		top :   { if styling.cardname_font == "MagicMedieval" then "33" else "29" }
		right:  278
		height: 23
		alignment: bottom shrink-overflow
		padding bottom: 0
		z index: 1
		font:
		font:
			name: 
				{ if styling.cardname_font == "Matrix" then "Matrix"
				else if styling.cardname_font == "MagicMedieval" then "MagicMedieval"
				else "Matrix" }
			size: 17
			color: rgb(255,255,255)
			shadow color: rgb(0,0,0)
			shadow displacement x: -1
			shadow displacement y: 1
	card symbol:
		left: {if card.card_symbol=="none" then 20 else 32}
		top : 31
		height: 20
		width: 14
		render style: image
		choice images:
			tombstone: tombstone.png
	casting cost:
		left: 280
		top : 32
		width: 63
		height: 23
		alignment: middle right
		symbol font:
			name: magic-mana-large
			size: 15
			alignment: middle right
		always symbol: true
		z index: 2
		padding top: 0
	############################# Image
	image:
		left:	32
		top:	62
		width:	311
		height:	{ if styling.image_size == "extended" then 408 else 228 }
		z index: 1
		mask:
			script: if styling.image_size == "extended" then "imagemask_extended.png" else "imagemask_standard.png"
	############################# Card type
	type:
		left: 35
		top : 297
		width: { 308 - max(22,card_style.rarity.content_width) }
		height: 20
		alignment: top shrink-overflow
		z index: 2
		padding top: 2
		font:
			name: Matrix
			size: 14
			color: rgb(255,255,255)
			shadow color: rgb(0,0,0)
			shadow displacement x: -1
			shadow displacement y: 1
			scale down to: 6
			separator color: rgb(192,192,192)
	rarity:
		right: 342
		top : 297
		width: 44
		height: 22
		z index: 1
		render style: image
		alignment: middle right
		choice images:
			# Images based on the set symbol
			basic land:
				script:
					if styling.inverted_common_symbol then symbol_variation(symbol: set.symbol, variation: "invertedcommon")
					else symbol_variation(symbol: set.symbol, variation: "common")
			common:
				script:
					if styling.inverted_common_symbol then symbol_variation(symbol: set.symbol, variation: "invertedcommon")
					else symbol_variation(symbol: set.symbol, variation: "common")
			uncommon:
				script:
					if styling.old_rarities
					then symbol_variation(symbol: set.symbol, variation: "olduncommon")
					else symbol_variation(symbol: set.symbol, variation: "uncommon")
			rare:
				script:
					if styling.old_rarities
					then symbol_variation(symbol: set.symbol, variation: "oldrare")
					else symbol_variation(symbol: set.symbol, variation: "rare")
			mythic rare:
				script:
					if styling.old_rarities
					then symbol_variation(symbol: set.symbol, variation: "oldmyth")
					else symbol_variation(symbol: set.symbol, variation: "mythic rare")
			special:    script: symbol_variation(symbol: set.symbol, variation: "special")
	############################# Text box
	text:
		left: 31
		top : 328
		width: 311
		height: 142
		font:
			name: MPlantin
			italic name: MPlantin-Italic
			size: 14
			scale down to: 6
			color: rgb(0,0,0)
		symbol font:
			name: { styling.text_box_mana_symbols }
			size: 14
		alignment:
			script:
				if styling.center_short_text and
				   not contains(match:"\n", card.rule_text) and
				   card.flavor_text == "<i-flavor></i-flavor>" and
				   card_style.text.content_lines <= 2
				then "middle center"
				else "middle left"
		z index: 2
		padding left:   6
		padding top:    2
		padding right:  4
		padding bottom: 2
		line height hard: 1.2
		line height line: 1.5
		line height soft: 0.9
		line height hard max: 1.3
		line height line max: 1.6
	watermark:
		left: 117
		top : 321
		width: 138
		height: 156
		z index: 1
		render style: image
		alignment: middle center
		include file: /magic-watermarks.mse-include/watermarks
	############################# PT
	pt:
		z index: 4
		left:	284
		top:	466
		width:	60
		height:	28
		alignment: center middle shrink-overflow
		font:
			name: ModMatrix
			size: 17
			weight: bold
			color: rgb(255,255,255)
			shadow color: rgb(0,0,0)
			shadow displacement x: -1
			shadow displacement y: 1
			separator color: rgb(200,0,0)
	############################# Card sorting / numbering
	############################# Copyright stuff
	illustrator:
		left: 57
		top : 479
		width: 217
		height: 16
		z index: 1
		font:
			name: Matrix
			size: 10
			weight: bold
			color: { font_color(card.card_color) }
	copyright line:
		left: 30
		top : 493
		width: 247
		height: 12
		z index: 1
		font:
			name: MPlantin
			size: 6.5
			color: { font_color(card.card_color) }
############################################################## Extra card fields
extra card field:
	type: choice
	name: pt box
	script: card.card_color
	editable: false
extra card style:
	pt box:
		left:	271
		top:	461
		width:	81
		height:	42
		z index: 1
		visible: { card.pt != "" }
		mask: ptmask.png
		render style: image
		image: { card_ptbox() }
