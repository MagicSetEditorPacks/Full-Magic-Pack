mse version:		2.1.2
game:				magic
short name:			Art Deco
full name:			Capenna Showcase
icon:				card_sample.png
position hint:		095

version:			2023-08-15
depends on:
	package:			magic.mse-game
	version:			2014-06-25
depends on:
	package:			magic-watermarks.mse-include
	version:			2007-09-23
depends on:
	package:			magic-identity-new.mse-include
	version:			2012-01-22
depends on:
	package:			magic-mana-large.mse-symbol-font
	version:			2007-09-23
depends on:
	package:			magic-mana-small.mse-symbol-font
	version:			2007-09-23
depends on:
	package:			magic-mainframe-extras.mse-include
	version:			2007-09-23

card width:			375
card height:		523
card dpi:			150


### blanks by Card Conjurer and GenevensiS
### code by GenevensiS


############################################################## Extra scripts

init script:
	horizontal_card_hybrid_2 :=
	{
		linear_blend(
			image1:	template(colors[0]),
			image2:	template(colors[1]),
			x1:		0.45,	y1:	0
			x2:		0.55,	y2: 0
		)
	}
	horizontal_card_hybrid :=
	[
		0:	horizontal_card_hybrid[0]
		1:	horizontal_card_hybrid[1]
		2:	horizontal_card_hybrid_2
		3:	horizontal_card_hybrid[3]
		4:	horizontal_card_hybrid[4]
		5:	horizontal_card_hybrid[5]
		6:	horizontal_card_hybrid[6]
		7:	horizontal_card_hybrid[7]
	]
	card_hybrid :=
	[
		radial:		card_hybrid["radial"]
		horizontal:	horizontal_card_hybrid
		overlay:	card_hybrid["overlay"]
		vertical:	card_hybrid["vertical"]
	]
	
	template_prefix :=
	[
		card:				"card/",
		pinlines_bottom:	"pinlines_bottom/",
		pinlines_top:		"pinlines_top/",
		textbox:			"textbox/",
		crown:				"crown/",
		stamprim:			"stamprim/",
		nonstamprim:		"nonstamprim/",
		identity:			"/magic-identity-new.mse-include/"
	]
	template_suffix :=
	[
		card:				"card.png",
		pinlines_bottom:	"pinlinesbottom.png",
		pinlines_top:		"pinlinestop.png",
		textbox:			"textbox.png",
		crown:				"crown.png",
		stamprim:			"stamprim.png",
		nonstamprim:		"nonstamprim.png",
		identity:			"identity.png"
	]
	template :=
	{
		if type == "typeline"				then crop_safe("typeline/" + input + "typeline.png",					offset_x: 0, offset_y: 500+move_typeline(), width: 744, height: 1039)
		else if type == "pinlines_typeline"	then crop_safe("pinlines_typeline/" + input + "pinlinestypeline.png",	offset_x: 0, offset_y: 500+move_typeline(), width: 744, height: 1039)
		else template_prefix[type] + input + template_suffix[type]
	}
	land_template		:=
	{
		if type == "typeline"				then crop_safe("typeline/" + (if input == "a" then "c" else input) + "typeline.png",					offset_x: 0, offset_y: 500+move_typeline(), width: 744, height: 1039)
		else if type == "pinlines_typeline"	then crop_safe("pinlines_typeline/" + (if input == "a" then "c" else input) + "pinlinestypeline.png",	offset_x: 0, offset_y: 500+move_typeline(), width: 744, height: 1039)
		else template_prefix[type] + (if input == "a" then "c" else input) + template_suffix[type]
	}
	
	card_background :=			{ color_background(input: card.card_color, type: "card",				base_hybrid: card_hybrid)		}@(offset:0 )
	card_typeline :=			{ color_background(input: card.card_color, type: "typeline",			base_hybrid: card_hybrid)		}@(offset:0 )
	card_pinlines_typeline :=	{ color_background(input: card.card_color, type: "pinlines_typeline",	base_hybrid: card_hybrid)		}@(offset:0 )
	card_pinlines_bottom :=		{ color_background(input: card.card_color, type: "pinlines_bottom",		base_hybrid: card_hybrid)		}@(offset:0 )
	card_pinlines_top :=		{ color_background(input: card.card_color, type: "pinlines_top",		base_hybrid: card_hybrid)		}@(offset:0 )
	card_textbox :=				{ color_background(input: card.card_color, type: "textbox",				base_hybrid: card_hybrid)		}@(offset:0 )
	card_crown :=				{ color_background(input: card.card_color, type: "crown",				base_hybrid: card_hybrid)		}@(offset:0 )
	card_stamprim :=			{ color_background(input: card.card_color, type: "stamprim",			base_hybrid: card_hybrid)		}@(offset:0 )
	card_nonstamprim :=			{ color_background(input: card.card_color, type: "nonstamprim",			base_hybrid: card_hybrid)		}@(offset:0 )
	card_stampholofoil :=
	{
		if not is_stamped() then "" else
		"holofoil/" +
		card.card_stamp +
		(if is_foil_stamped() then "" else "_void") +
		".png"
	}
	card_loyaltycostbox :=
	{
		cost := card["loyalty_cost_" + input]
		if		contains(cost, match: "+") then	"loyalty/loyup.png"
		else if	contains(cost, match: "-") then	"loyalty/loydown.png"
		else									"loyalty/loynil.png"
	}
	
	textbox_image :=
	{
		base_image :=	if	styling.textbox_color == rgb(255,255,254) then	card_textbox()
						else												recolor_image("textbox_background.png", color: styling.textbox_color)
		set_alpha(base_image, alpha: get_alpha(styling.textbox_opacity_percentage, default: 70))
	}
	
	get_alpha :=
	{
		alpha_value :=	if input == "" then default
						else to_number(input) or else default
		if alpha_value < 0 then 0.0
		else if alpha_value <= 100 then alpha_value/100.0
		else 1.0
	}
	
	pure_colors :=
	{
		sort_text(
			order: if card_is_wedge(input) then "(wbgur)" else "(wubrg)",
			(if chosen(card.card_color, choice:"white")	then "w") +
			(if chosen(card.card_color, choice:"blue")	then "u") +
			(if chosen(card.card_color, choice:"black")	then "b") +
			(if chosen(card.card_color, choice:"red")	then "r") +
			(if chosen(card.card_color, choice:"green")	then "g")
		)
	}
	
	mana_t :=
	{
		if      styling.tap_symbol == "old"        then "old"
		else if styling.tap_symbol == "diagonal T" then "older"
		else                                            "new"
	}
	guild_mana		:= { styling.use_guild_mana_symbols }
	ancestral_mana	:= { styling.use_ancestral_mana_symbols }
	
	has_identity	:= { styling.color_indicator_dot }
	has_identity_2	:= { false }
	
	is_promo :=			{ styling.promo }
	is_unsorted :=		{ styling.remove_from_autocount }
	is_flavor_barred :=	{ contains(styling.textbox_layout, match: "flavor") and not contains(styling.textbox_layout, match: "only flavor") and styling.flavor_bar and remove_tags(card.flavor_text) != "" }
	is_stamped :=		{ card.card_stamp != "none" and (contains(styling.holofoil_stamps, match: "yes") or (styling.holofoil_stamps == "only rares and mythics" and is_rare()))}
	is_foil_stamped :=	{ styling.holofoil_stamps == "yes, all foil" or is_rare() }
	is_crowned :=		{ styling.legend_crown == "yes" or (styling.legend_crown == "if legendary" and lang_setting("is_legendary")(card.super_type)) }
	
	mainframe_walker :=
	{
		true
	}
	
	mainframe_walker_text_script :=
	{
		count := abilities_count()
		if		count == 6 then	combined_editor(field1: card.level_1_text, separator1: "<line>\n</line>", field2: card.level_2_text, separator2: "<line>\n</line>", field3: card.level_3_text, separator3: "<line>\n</line>", field4: card.level_4_text, separator4: "<line>\n</line>", field5: card.level_5_text, separator5: "<line>\n</line>", field6: card.level_6_text)
		else if	count == 5 then	combined_editor(field1: card.level_1_text, separator1: "<line>\n</line>", field2: card.level_2_text, separator2: "<line>\n</line>", field3: card.level_3_text, separator3: "<line>\n</line>", field4: card.level_4_text, separator4: "<line>\n</line>", field5: card.level_5_text)
		else if	count == 4 then	combined_editor(field1: card.level_1_text, separator1: "<line>\n</line>", field2: card.level_2_text, separator2: "<line>\n</line>", field3: card.level_3_text, separator3: "<line>\n</line>", field4: card.level_4_text)
		else if	count == 3 then	combined_editor(field1: card.level_1_text, separator1: "<line>\n</line>", field2: card.level_2_text, separator2: "<line>\n</line>", field3: card.level_3_text)
		else if	count == 2 then	combined_editor(field1: card.level_1_text, separator1: "<line>\n</line>", field2: card.level_2_text)
		else 					forward_editor(field: card.level_1_text)
	}
	
	margin_left :=
	{
		if		input == "lv1" then	(if card.loyalty_cost_1 == "" then 0 else 200)
		else if	input == "lv2" then	(if card.loyalty_cost_2 == "" then 0 else 200)
		else if	input == "lv3" then	(if card.loyalty_cost_3 == "" then 0 else 200)
		else if	input == "lv4" then	(if card.loyalty_cost_4 == "" then 0 else 200)
		else if	input == "lv5" then	(if card.loyalty_cost_5 == "" then 0 else 200)
		else if	input == "lv6" then	(if card.loyalty_cost_6 == "" then 0 else 200)
		else						0
	}
	
	text_filter := text_filter + { apply_margins(input, name:margin_code) } + { add_spacers(input, name:margin_code) }
	
	add_spacers :=
	{
		#The 'then ""' has a zero-width space inside
		if input == "" and level_is_active(name) then "​"
		else input
	}
	
	level_is_active :=
	{
		if		input == "lv6" then	abilities_count() >= 6
		else if	input == "lv5" then	abilities_count() >= 5
		else if	input == "lv4" then	abilities_count() >= 4
		else if	input == "lv3" then	abilities_count() >= 3
		else if	input == "lv2" then	abilities_count() >= 2
		else						true
	}
	
	ability_top :=
	{
		if card_style.text.layout.blocks[input-1].bottom or else 0 > 0 then to_int(card_style.text.top) + to_int(card_style.text.layout.blocks[input-1].top)
		else 600
	}
	
	ability_center :=
	{
		if card_style.text.layout.blocks[input-1].bottom or else 0 > 0 then to_int(card_style.text.top) + to_int(card_style.text.layout.blocks[input-1].middle)
		else 600
	}
	
	ability_bottom :=
	{
		if card_style.text.layout.blocks[input-1].bottom or else 0 > 0 then to_int(card_style.text.top) + to_int(card_style.text.layout.blocks[input-1].bottom)
		else 600
	}
	
	abilities_count :=
	{
		if		styling.number_of_textboxes == "three"	then	3
		else if	styling.number_of_textboxes == "four"	then	4
		else if	styling.number_of_textboxes == "two"	then	2
		else if	styling.number_of_textboxes == "five"	then	5
		else if	styling.number_of_textboxes == "one"	then	1
		else													6
	}
	
	loyal_move :=
	{
		text := pull_comma_array(styling.move_loyalty_costs, cell:input-1, end:0, default:0)
		if text == "-" then text := "0"
		clamp(text, minimum: -300, maximum: 300)
	}
	
	loyal_correction :=
	{
		cost := card["loyalty_cost_" + input]
		if		contains(cost, match: "+") then	1
		else if	contains(cost, match: "-") then	-2
		else									0
	}
	
	separator_move :=
	{
		text := pull_comma_array(styling.move_separators, cell:input-1, end:0, default:0)
		if text == "-" then text := "0"
		clamp(text, minimum: -300, maximum: 300)
	}
	
	textbox_background_top :=		{ 327 - move_typeline()*523/1039 }
	textbox_background_bottom :=	{ 483 }
	
	stripe_height :=	{ 8 }
	stripe_top :=		{ to_int((ability_bottom(input) + ability_top(input+1) - stripe_height())/2) + separator_move(input) }
	
	move_typeline :=	{ clamp(if styling.move_typeline == "-" then 0 else to_int(styling.move_typeline), minimum: -200, maximum: 410) }
	
	chop_top :=			{ split := split_comma(styling.chop_textbox); if length(split) > 0 and split[0] != "" and split[0] != "-" then clamp(split[0], maximum:300, minimum:-300) else 0 }
	chop_bottom :=		{ split := split_comma(styling.chop_textbox); if length(split) > 1 and split[1] != "" and split[1] != "-" then clamp(split[1], maximum:300, minimum:-300) else 0 }
	chop_right :=		{ split := split_comma(styling.chop_textbox); if length(split) > 2 and split[2] != "" and split[2] != "-" then clamp(split[2], maximum:300, minimum:-300) else 0 }
	
	shrink_name :=	{ if styling.shrink_name_text == "-" then 0 else if styling.shrink_name_text != "" then clamp(styling.shrink_name_text, minimum: -20, maximum: 12) else 0 }
	shrink_type :=	{ if styling.shrink_type_text == "-" then 0 else if styling.shrink_type_text != "" then clamp(styling.shrink_type_text, minimum: -20, maximum: 10) else 0 }
	
	use_main_rarity	:= { contains(set.mainframe_rarity_name, match: ".png") }
	
	alt_rarity := { styling.alt_rarity_color != "" }
	alt_rarity_color :=
	{
		string := "83,67,53:177,150,131:0,0,0:0,0,0:0.07:"
		if match(styling.alt_rarity_color, match:":$") then string := styling.alt_rarity_color
		colons := length(filter_text(string, match:":"))
		output := split_text(string, match:",|:")
		splits := split_text(string, match:":")
		if colons == 4
			then output := split_text(string+"0.07:", match:",|:")
		if colons == 3
			then output := split_text(string+splits[2]+"0.07:", match:",|:")
		else if colons == 2
			then output := split_text(string+"0,0,0:0,0,0:0.07:", match:",|:")
		else if colons == 1
			then output := split_text(string+splits[0]+":0,0,0:0,0,0:0.07:", match:",|:")
		output
	}
	alt_symbol :=
	{
		colors := alt_rarity_color()
		symbol_variation(
			symbol:			set.symbol,
			border_radius:	colors.12,
			fill_type:		"linear gradient",
			fill_color_1:	rgb(colors.0, colors.1, colors.2),
			fill_color_2:	rgb(colors.3, colors.4, colors.5),
			border_color_1:	rgb(colors.6, colors.7, colors.8),
			border_color_2:	rgb(colors.9, colors.10, colors.11),
			center_x:		0.5,
			center_y:		0.5,
			end_x:			1,
			end_y:			1
		)
	}
	
	rarity_right :=		{ split := split_comma(styling.rarity_coordinates); if length(split) > 0 and split.0 != "" then clamp(split.0, minimum: 0, maximum: 600) else 344 }
	rarity_top :=		{ split := split_comma(styling.rarity_coordinates); if length(split) > 1 and split.1 != "" then clamp(split.1, minimum: 0, maximum: 800) else 295 - move_typeline()*523/1039 }
	rarity_size :=		{ split := split_comma(styling.rarity_coordinates); if length(split) > 2 and split.2 != "" then clamp(split.2, minimum: 0, maximum: 800) else 26 }
	rarity_left :=		{ rarity_right() - rarity_size() }
	
	popout_left :=		{ split := split_comma(styling.popout_art_coordinates); if length(split) > 3 and split.0 != "" then clamp(split.0, minimum: -300,	maximum: 700) else 0 }
	popout_top :=		{ split := split_comma(styling.popout_art_coordinates); if length(split) > 3 and split.1 != "" then clamp(split.1, minimum: -300,	maximum: 1000) else 0 }
	popout_width :=		{ split := split_comma(styling.popout_art_coordinates); if length(split) > 3 and split.2 != "" then clamp(split.2, minimum: 0,		maximum: 700) else 0 }
	popout_height :=	{ split := split_comma(styling.popout_art_coordinates); if length(split) > 3 and split.3 != "" then clamp(split.3, minimum: 0,		maximum: 1000) else 0 }
	
	split_comma := split_text@(match: " *, *")
	
	un_png := replace@(match:".png", replace: "")
	
	clamp :=
	{
		number := to_number(input)
		if number < minimum then minimum
		else if number > maximum then maximum
		else number
	}
	
	crop_safe :=
	{
		if		offset_x < 0
			or	offset_y < 0
			or	width < 1
			or	height < 1
			or	offset_x + width > 744
			or	offset_y + height > 3000
		then ""
		else crop(input, offset_x: offset_x, offset_y: offset_y, width: width, height: height)
	}
	
	card_mask :=
	{
		if styling.move_typeline == "" then "card_mask.png" else
		combine_blend(
			image1: combine_blend(
				image1: crop_safe("crop_mask_top.png", offset_x: 0, offset_y: 917+move_typeline(), width: 744, height: 1039),
				image2: "card_typeline_mask.png",
				combine: "multiply"
			)
			image2: "card_mask.png",
			combine: "add"
		)
	}
	
	pinlines_mask :=
	{
		combine_blend(
			image1: crop_safe("crop_mask_top.png", offset_x: 0, offset_y: 1500-top, width: 744, height: 1039),
			image2: crop_safe("crop_mask_bottom.png", offset_x: 0, offset_y: 1500-bottom, width: 744, height: 1039),
			combine: "multiply"
		)
	}


############################################################## Set info fields

set info style:
	symbol:
		variation:
			name:				invertedcommon
			border radius:		0.10
			fill type:			solid
			fill color:			rgb(255,255,255)
			border color:		rgb(0,0,0)


############################################################## Extra style options

styling field:
	type:			text
	name:			popout art coordinates
	description:	The coordinates for the popout image field, formatted as "left,top,width,height". Used to make the illustration jump in front of the frame.
styling field:
	type:			boolean
	name:			stretch art to whole card
	description:	Make the image span the whole card instead of just the visible part. Use this to help make popout effects.
	initial:		no
styling field:
	type:			text
	name:			shrink name text
	description:	Shrinks name X pixels.
styling field:
	type:			text
	name:			shrink type text
	description:	Shrinks type X pixels.
styling field:
	type:			text
	name:			move typeline
	description:	Move the typeline up X pixels. Use negatives to move down.
styling field:
	type:			text
	name:			textbox opacity percentage
	description:	Set the opacity percentage for textboxes and stripes. Opaque is 100, transparent is 0, default is 70.
styling field:
	type:			color
	name:			textbox color
	description:	Use the specified color for the textbox background.
	choice:
		name:			black
		color:			rgb(0,0,0)
	choice:
		name:			white
		color:			rgb(255,255,255)
	choice:
		name:			use card color
		color:			rgb(255,255,254)
	initial:		rgb(255,255,254)
styling field:
	type:			choice
	name:			number of textboxes
	description:	Number of textboxes for abilities and passives. Modify content of textboxes if this has trouble updating after being changed.
	choice:			one
	choice:			two
	choice:			three
	choice:			four
	choice:			five
	choice:			six
	initial:		three
styling field:
	type:			text
	name:			move separators
	description:	Moves the limits between abilities down by this many pixels. Use negatives to move up. Formatted as "1,2,3,4,5"
styling field:
	type:			text
	name:			move loyalty costs
	description:	Moves loyalty costs down by this many pixels. Use negatives to move up. Formatted as "1,2,3,4,5,6"
styling field:
	type:			text
	name:			rule text font size cap
	description:	Caps the font size of the rules text and the flavor text. Default is 26.
styling field:
	type:			color
	name:			rule text color
	description:	Use the specified color for the rule text.
	choice:
		name:			black
		color:			rgb(0,0,0)
	choice:
		name:			white
		color:			rgb(255,255,255)
	initial:		black
styling field:
	type:			choice
	name:			center rule text
	description:	When to center text (short text means only on one-line cards with no flavor text)
	choice:			always
	choice:			short text only
	choice:			never
	initial:		never
styling field:
	type:			text
	name:			chop textbox
	description:	Formatted as "X,Y". Shrinks textbox X pixels from the top and Y from the bottom.
styling field:
	type:			choice
	name:			legend crown
	description:	Use the legend crown?
	choice:			yes
	choice:			if legendary
	choice:			no
	initial:		if legendary
styling field:
	type:			boolean
	name:			color indicator dot
	description:	Use a dot in the typeline to indicate card color?
	initial:		no
styling field:
	type:			choice
	name:			holofoil stamps
	description:	Use the security holofoil stamps?
	choice:			yes, all foil
	choice:			yes, un/commons not foil
	choice:			only rares and mythics
	choice:			no
	initial:		only rares and mythics
styling field:
	type:			package choice
	name:			overlay
	description:	Should there be an overlay applied, such as foil?
	match:			magic-overlay-*.mse-include
	required:		false
styling field:
	type:			boolean
	name:			inverted common symbol
	description:	Should the common rarity symbol be inverted, like in Coldsnap?
	initial:		no
styling field:
	type:			text
	name:			alt rarity color
	description:	Use a custom color for the rarity symbol. Formatted as "R1,G1,B1:R2,G2,B2:..." 1 and 2 are fill color, 3 and 4 border color
styling field:
	type:			text
	name:			rarity coordinates
	description:	The coordinates for the set symbol, formatted as "right,top,size". Default is 344,295,26.
styling field:
	type:			choice
	name:			corners
	description:	What should the corners of the card look like?
	choice:			rounded and antialiased
	choice:			rounded
	choice:			none
	initial:		rounded and antialiased
styling field:
	type:			boolean
	name:			remove from autocount
	description:	Removes the automatic card number for specific cards, to allow for overcounted cards like in Planeswalker Decks.
	initial:		no
styling field:
	type:			boolean
	name:			promo
	description:	Is this card a promo card, with the "P" rarity?
	initial:		no
styling field:
	type:			color
	name:			copyright text color
	description:	Color of the copyright, illustrator, and set code text.
	choice:			white
	choice:			black
	initial:		white
	allow custom:	true
styling field:
	type:			boolean
	name:			use ancestral mana symbols
	description:	Enables ancestral's modified generic mana.
	initial:		no
styling field:
	type:			boolean
	name:			use guild mana symbols
	description:	Use the Ravnica guild symbols instead of the official half/half circles for hybrid mana.
	initial:		no
styling field:
	type:			choice
	name:			tap symbol
	description:	What tap and untap symbols should be used on cards?
	choice:			modern
	choice:			old
	choice:			diagonal T
	initial:		modern
styling field:
	type:			package choice
	name:			casting cost mana symbols
	description:	Symbols used in the casting cost.
	match:			magic-mana-*.mse-symbol-font
	initial:		magic-mana-large.mse-symbol-font
styling field:
	type:			package choice
	name:			text box mana symbols
	description:	Symbols used in the rule text and flavor text.
	match:			magic-mana-*.mse-symbol-font
	initial:		magic-mana-small.mse-symbol-font
styling field:
	type:			text
	name:			name font
	description:	Font used for the name, type, and pt. Hover your mouse over the font file to see it's details, and write it's Title here.
styling field:
	type:			text
	name:			text font
	description:	Font used for the rule text. Hover your mouse over the font file to see it's details, and write it's Title here.
styling field:
	type:			text
	name:			text italic font
	description:	Font used for the flavor text and reminder texts. Hover your mouse over the font file to see it's details, and write it's Title here.
styling style:
	use guild mana symbols:
		choice images:
			yes:				/magic-mana-small.mse-symbol-font/mana_guild_rg.png
			no:					/magic-mana-small.mse-symbol-font/mana_rg.png
	tap symbol:
		render style:	both
		choice images:
			modern:				/magic-mana-large.mse-symbol-font/mana_t.png
			old:				/magic-mana-large.mse-symbol-font/mana_t_old.png
			diagonal T:			/magic-mana-large.mse-symbol-font/mana_t_older.png
	center rule text:
		render style:	both
		choice images:
			always:				/magic.mse-game/icons/center-all-text.png
			short text only:	/magic.mse-game/icons/center-short-text.png
			never:				/magic.mse-game/icons/center-no-text.png
	inverted common symbol:
		choice images:
			no:					{ symbol_variation(symbol: set.symbol, variation: "common") }
			yes:				{ symbol_variation(symbol: set.symbol, variation: "invertedcommon") }


############################################################## Card fields

card style:
	############################# Background stuff
	border color:
		left:				0
		top :				0
		width:				{ stylesheet.card_width }
		height:				{ stylesheet.card_height }
		z index:			100
		mask:				border_mask.png
	card color:
		left:				0
		top:				0
		width:				{ stylesheet.card_width }
		height:				{ stylesheet.card_height }
		z index:			210
		popup style:		in place
		render style:		image
		image:				{ card_background() }
		mask:				{ card_mask() }
	############################# Name line
	name:
		left:				31
		top:				27
		right:				{ 343 - (if card_style.casting_cost.content_width == 0 then 0 else card_style.casting_cost.content_width + 4) }
		height:				26
		z index:			1000
		alignment:			middle left shrink-overflow
		font:
			name:				{ if styling.name_font == "" then "Beleren Bold" else styling.name_font }
			size:				{ 15 - shrink_name() }
			color:				white
	casting cost:
		right:				346
		top:				27
		width:				{ max(35, card_style.casting_cost.content_width + 6) }
		height:				27
		z index:			1010
		alignment:			middle right
		always symbol:		true
		font:
			name:				MPlantin
			size:				15
			color:				black
		symbol font:
			name:				{ styling.casting_cost_mana_symbols }
			size:				15
	############################# Image
	image:
		left:				{ if styling.stretch_art_to_whole_card then 0 else 16 }
		top:				{ if styling.stretch_art_to_whole_card then 0 else 53 }
		width:				{ if styling.stretch_art_to_whole_card then 375 else 343 }
		height:				{ if styling.stretch_art_to_whole_card then 523 else if get_alpha(styling.textbox_opacity_percentage, default: 70) > 0.995 then 240-move_typeline()*523/1039 else 429 }
		z index:			0
	mainframe image:
		left:				{ popout_left() }
		top:				{ popout_top() }
		width:				{ popout_width() }
		height:				{ popout_height() }
		z index:			2000
	############################# Card type
	indicator:
		left:				29
		top:				{ 300 - move_typeline()*523/1039 }
		width:				17
		height:				17
		z index:			1000
		render style:		image
		visible:			{ has_identity() }
		image:				{ card_identity() }
	type:
		left:				{ if has_identity() then 50 else 32 }
		top:				{ 294 - move_typeline()*523/1039 }
		right:				{ if set.shorten_types_for_rarity then max(180,min(343, rarity_left()-2)) else 343 }
		height:				28
		z index:			1000
		alignment:			middle left shrink-overflow
		font:
			name:				{ if styling.name_font == "" then "Beleren Bold" else styling.name_font }
			size:				{ 13 - shrink_type() }
			color:				white
			separator color:	red
	rarity:
		right:				{ rarity_right() }
		top:				{ rarity_top() }
		width:				{ rarity_size() }
		height:				{ rarity_size() }
		z index:			1010
		alignment:			middle right
		render style:		image
		choice images:
			# Images based on the set symbol
			basic land:
				script:
					if use_main_rarity() then "/magic-mainframe-extras.mse-include/" + un_png(set.mainframe_rarity_name) + "c.png"
					else if alt_rarity() then alt_symbol()
					else if styling.inverted_common_symbol then symbol_variation(symbol: set.symbol, variation: "invertedcommon")
					else symbol_variation(symbol: set.symbol, variation: "common")
			common:
				script:
					if use_main_rarity() then "/magic-mainframe-extras.mse-include/" + un_png(set.mainframe_rarity_name) + "c.png"
					else if alt_rarity() then alt_symbol()
					else if styling.inverted_common_symbol then symbol_variation(symbol: set.symbol, variation: "invertedcommon")
					else symbol_variation(symbol: set.symbol, variation: "common")
			uncommon:
				script:
					if use_main_rarity() then "/magic-mainframe-extras.mse-include/" + un_png(set.mainframe_rarity_name) + "u.png"
					else if alt_rarity() then alt_symbol()
					else symbol_variation(symbol: set.symbol, variation: "uncommon")
			rare:
				script:
					if use_main_rarity() then "/magic-mainframe-extras.mse-include/" + un_png(set.mainframe_rarity_name) + "r.png"
					else if alt_rarity() then alt_symbol()
					else symbol_variation(symbol: set.symbol, variation: "rare")
			mythic rare:
				script:
					if use_main_rarity() then "/magic-mainframe-extras.mse-include/" + un_png(set.mainframe_rarity_name) + "m.png"
					else if alt_rarity() then alt_symbol()
					else symbol_variation(symbol: set.symbol, variation: "mythic rare")
			special:
				script:
					if use_main_rarity() then "/magic-mainframe-extras.mse-include/" + un_png(set.mainframe_rarity_name) + "s.png"
					else if alt_rarity() then alt_symbol()
					else symbol_variation(symbol: set.symbol, variation: "special")
			masterpiece:
				script:
					if use_main_rarity() then "/magic-mainframe-extras.mse-include/" + un_png(set.mainframe_rarity_name) + "mp.png"
					else symbol_variation(symbol: set.masterpiece_symbol, variation: "mythic rare")
	############################# Text box
	text:
		left:				49
		top:				{ 330 + chop_top() - move_typeline()*523/1039 + if is_stamped() then 3 else 0 }
		right:				{ 342 - chop_right() }
		bottom:				{ 479 - chop_bottom() - if is_stamped() then 3 else 0 }
		z index:			1000
		line height hard:	{ if is_modal(card.rule_text) then 0.9 else 1.2 }
		line height line:	1.5
		line height soft:	0.9
		line height hard max:	{ if is_modal(card.rule_text) then 1.0 else 1.3 }
		line height line max:	2.2
		alignment:
			script:
				if (styling.center_rule_text == "short text only" and
				   not contains(match:"\n", card.rule_text) and
				   card.flavor_text == "<i-flavor></i-flavor>" and
				   card_style.text.content_lines <= 2) or
				   styling.center_rule_text == "always"
				then "middle center"
				else "middle left"
		font:
			name:				{ if styling.text_font == "" then "MPlantin" else styling.text_font }
			italic name:		{ if styling.text_italic_font == "" then "MPlantin-Italic" else styling.text_italic_font }
			size:				{ if styling.rule_text_font_size_cap != "" then to_number(styling.rule_text_font_size_cap) else 13 }
			scale down to:		7
			color:				{ styling.rule_text_color }
		symbol font:
			name:				{ styling.text_box_mana_symbols }
			size:				{ if styling.rule_text_font_size_cap != "" then to_number(styling.rule_text_font_size_cap) else 13 }
			scale down to:		7
	watermark:
		left:				10
		top:				{ move := move_typeline()*523/1039; if move > 0 then 331 - move/2 else 331 - move}
		right:				365
		bottom:				{ min(475, 475 - move_typeline()*523/1039/2) }
		z index:			600
		render style:		image
		popup style:		in place
		alignment:			middle center
		include file:		/magic-watermarks.mse-include/watermarks
	loyalty cost 1:
		left:				26
		top:				{ ability_center(1) - 12 + loyal_move(1) + loyal_correction(1) }
		width:				{ if abilities_count() >= 1 then 26 else 0 }
		height:				24
		z index:			1010
		alignment:			center middle shrink-overflow
		font:
			name:				{ if styling.name_font == "" then "Beleren Bold" else styling.name_font }
			size:				12
			scale down to:		7
			color:				white
	loyalty cost 2:
		left:				26
		top:				{ ability_center(2) - 12 + loyal_move(2) + loyal_correction(2) }
		width:				{ if abilities_count() >= 2 then 26 else 0 }
		height:				24
		z index:			1010
		alignment:			center middle shrink-overflow
		font:
			name:				{ if styling.name_font == "" then "Beleren Bold" else styling.name_font }
			size:				12
			scale down to:		7
			color:				white
	loyalty cost 3:
		left:				26
		top:				{ ability_center(3) - 12 + loyal_move(3) + loyal_correction(3) }
		width:				{ if abilities_count() >= 3 then 26 else 0 }
		height:				24
		z index:			1010
		alignment:			center middle shrink-overflow
		font:
			name:				{ if styling.name_font == "" then "Beleren Bold" else styling.name_font }
			size:				12
			scale down to:		7
			color:				white
	loyalty cost 4:
		left:				26
		top:				{ ability_center(4) - 12 + loyal_move(4) + loyal_correction(4) }
		width:				{ if abilities_count() >= 4 then 26 else 0 }
		height:				24
		z index:			1010
		alignment:			center middle shrink-overflow
		font:
			name:				{ if styling.name_font == "" then "Beleren Bold" else styling.name_font }
			size:				12
			scale down to:		7
			color:				white
	loyalty cost 5:
		left:				26
		top:				{ ability_center(5) - 12 + loyal_move(5) + loyal_correction(5) }
		width:				{ if abilities_count() >= 5 then 26 else 0 }
		height:				24
		z index:			1010
		alignment:			center middle shrink-overflow
		font:
			name:				{ if styling.name_font == "" then "Beleren Bold" else styling.name_font }
			size:				12
			scale down to:		7
			color:				white
	loyalty cost 6:
		left:				26
		top:				{ ability_center(6) - 12 + loyal_move(6) + loyal_correction(6) }
		width:				{ if abilities_count() >= 6 then 26 else 0 }
		height:				24
		z index:			1010
		alignment:			center middle shrink-overflow
		font:
			name:				{ if styling.name_font == "" then "Beleren Bold" else styling.name_font }
			size:				12
			scale down to:		7
			color:				white
	############################# Loyalty
	loyalty:
		left:				308
		top:				466
		width:				42
		height:				26
		z index:			1000
		alignment:			center middle shrink-overflow
		font:
			name:				{ if styling.name_font == "" then "Beleren Bold" else styling.name_font }
			size:				17
			color:				white
			separator color:	red
	############################# Copyright stuff
	card stamp:
		left:				0
		top:				0
		width:				{ stylesheet.card_width }
		height:				{ stylesheet.card_height }
		z index:			720
		render style:		image
		popup style:		in place
		image:				{ card_stampholofoil() }
		mask:				stamp_holofoil_mask.png
	set code:
		left:				24
		top:				499
		width:				40
		height:				10
		z index:			1000
		alignment:			middle left
		font:
			name:				Relay-Medium
			size:				7
			color:				{ styling.copyright_text_color }
			weight:				bold
	illustrator:
		left:				{ if card.set_code == "" then 38 else 42 + card_style.set_code.content_width }
		top:				498.5
		width:				200
		height:				10
		z index:			1000
		alignment:			middle left
		font:
			name:				Beleren Small Caps Bold
			size:				7.25
			color:				{ styling.copyright_text_color }
	copyright:
		right:				351
		top:				{ if card.pt != "" then 499 else 494 }
		width:				{ if set.automatic_copyright then 0 else 140 }
		height:				10
		z index:			1000
		alignment:			middle right shrink-overflow
		font:
			name:				Matrix
			size:				7
			color:				{ styling.copyright_text_color }
			weight:				bold


############################################################## Extra card fields

extra card field:
	type:				text
	name:				card code
	save value:			false
	show statistics:	false
	script:
		if set.automatic_card_numbers and not styling.remove_from_autocount
		then forward_editor(prefix: card_number_m15() + "/" + card_count_m15() + " " + rarity_code() + " ", field: card.card_code_text)
		else combined_editor(field1: card.custom_card_number, separator: (if card.custom_card_number != "" then " " else "") + rarity_code() + " ", field2: card.card_code_text)
extra card field:
	type:				text
	name:				auto copyright
	script:				set.copyright
	editable:			false
	save value:			false
	show statistics:	false
extra card field:
	type:				choice
	name:				artist arrow
	editable:			false
	save value:			false
	show statistics:	false
	choice:				artist arrow
extra card field:
	type:				choice
	name:				pinlines top
	script:				card.card_color
	editable:			false
	save value:			false
	show statistics:	false
extra card field:
	type:				choice
	name:				pinlines bottom
	script:				card.card_color
	editable:			false
	save value:			false
	show statistics:	false
extra card field:
	type:				choice
	name:				pinlines typeline
	script:				card.card_color
	editable:			false
	save value:			false
	show statistics:	false
extra card field:
	type:				choice
	name:				typeline
	script:				card.card_color
	editable:			false
	save value:			false
	show statistics:	false
extra card field:
	type:				color
	name:				pinlines border
	script:				card.border_color
	editable:			false
	save value:			false
	show statistics:	false
extra card field:
	type:				choice
	name:				crown
	script:				card.card_color
	editable:			false
	save value:			false
	show statistics:	false
extra card field:
	type:				color
	name:				crown border
	script:				card.border_color
	editable:			false
	save value:			false
	show statistics:	false
extra card field:
	type:				choice
	name:				loyalty box
	script:				card.card_color
	editable:			false
	save value:			false
	show statistics:	false
extra card field:
	type:				choice
	name:				loyalty cost box 1
	editable:			false
	save value:			false
	choice:				loyalty cost box 1
extra card field:
	type:				choice
	name:				loyalty cost box 2
	editable:			false
	save value:			false
	choice:				loyalty cost box 2
extra card field:
	type:				choice
	name:				loyalty cost box 3
	editable:			false
	save value:			false
	choice:				loyalty cost box 3
extra card field:
	type:				choice
	name:				loyalty cost box 4
	editable:			false
	save value:			false
	choice:				loyalty cost box 4
extra card field:
	type:				choice
	name:				loyalty cost box 5
	editable:			false
	save value:			false
	choice:				loyalty cost box 5
extra card field:
	type:				choice
	name:				loyalty cost box 6
	editable:			false
	save value:			false
	choice:				loyalty cost box 6
extra card field:
	type:				text
	name:				colon 1
	editable:			false
	save value:			false
	script:				":"
extra card field:
	type:				text
	name:				colon 2
	editable:			false
	save value:			false
	script:				":"
extra card field:
	type:				text
	name:				colon 3
	editable:			false
	save value:			false
	script:				":"
extra card field:
	type:				text
	name:				colon 4
	editable:			false
	save value:			false
	script:				":"
extra card field:
	type:				text
	name:				colon 5
	editable:			false
	save value:			false
	script:				":"
extra card field:
	type:				text
	name:				colon 6
	editable:			false
	save value:			false
	script:				":"
extra card field:
	type:				choice
	name:				textbox stripe separator 1
	editable:			false
	save value:			false
	choice:				textbox stripe separator 1
extra card field:
	type:				choice
	name:				textbox stripe separator 2
	editable:			false
	save value:			false
	choice:				textbox stripe separator 2
extra card field:
	type:				choice
	name:				textbox stripe separator 3
	editable:			false
	save value:			false
	choice:				textbox stripe separator 3
extra card field:
	type:				choice
	name:				textbox stripe separator 4
	editable:			false
	save value:			false
	choice:				textbox stripe separator 4
extra card field:
	type:				choice
	name:				textbox stripe separator 5
	editable:			false
	save value:			false
	choice:				textbox stripe separator 5
extra card field:
	type:				choice
	name:				textbox background
	editable:			false
	save value:			false
	show statistics:	false
	choice:				textbox background
extra card field:
	type:				choice
	name:				textbox stripe 1
	editable:			false
	save value:			false
	show statistics:	false
	choice:				textbox stripe 1
extra card field:
	type:				choice
	name:				textbox stripe 2
	editable:			false
	save value:			false
	show statistics:	false
	choice:				textbox stripe 2
extra card field:
	type:				choice
	name:				textbox stripe 3
	editable:			false
	save value:			false
	show statistics:	false
	choice:				textbox stripe 3
extra card field:
	type:				choice
	name:				card stamp rim
	script:				card.card_color
	editable:			false
	save value:			false
	show statistics:	false
extra card field:
	type:				color
	name:				card stamp border
	script:				card.border_color
	editable:			false
	save value:			false
	show statistics:	false
extra card field:
	type:				choice
	name:				foil layer
	editable:			false
	save value:			false
	show statistics:	false
	choice:				foil layer
extra card field:
	type:				color
	name:				corners
	script:				rgb(255, 255, 255)
	editable:			false
	save value:			false
	show statistics:	false

extra card style:
	card code:
		left:				24
		top:				489
		width:				120
		height:				10
		z index:			1000
		font:
			name:				Relay-Medium
			size:				7
			color:				{ styling.copyright_text_color }
			weight:				bold
	auto copyright:
		right:				351
		top:				{ if card.pt != "" then 499 else 494 }
		width:				{ if set.automatic_copyright then 140 else 0 }
		height:				10
		z index:			1000
		alignment:			middle right shrink-overflow
		font:
			name:				Matrix
			size:				7
			color:				{ styling.copyright_text_color }
			weight:				bold
	artist arrow:
		left:				{ if card.set_code == "" then 25 else 29 + card_style.set_code.content_width }
		top:				501
		width:				12
		height:				7
		z index:			1000
		render style:		image
		image:				{ recolor_image("artist_arrow.png", color: styling.copyright_text_color) }
	pinlines top:
		left:				0
		top:				0
		width:				{ stylesheet.card_width }
		height:				{ stylesheet.card_height }
		z index:			200
		render style:		image
		image:				{ card_pinlines_top() }
		mask:				{ pinlines_mask(top: 135, bottom: 553-move_typeline()) }
	pinlines bottom:
		left:				0
		top:				0
		width:				{ stylesheet.card_width }
		height:				{ stylesheet.card_height }
		z index:			200
		render style:		image
		image:				{ card_pinlines_bottom() }
		mask:				{ pinlines_mask(top: 680-move_typeline(), bottom: 883) }
	pinlines typeline:
		left:				0
		top:				0
		width:				{ stylesheet.card_width }
		height:				{ stylesheet.card_height }
		z index:			210
		render style:		image
		image:				{ card_pinlines_typeline() }
	typeline:
		left:				0
		top:				0
		width:				{ stylesheet.card_width }
		height:				{ stylesheet.card_height }
		z index:			210
		render style:		image
		image:				{ card_typeline() }
	pinlines border:
		left:				0
		top:				0
		width:				{ stylesheet.card_width }
		height:				{ stylesheet.card_height }
		z index:			100
		mask:				{ crop_safe("border_pinlines_mask.png", offset_x: 0, offset_y: 500+move_typeline(), width: 744, height: 1039) }
	crown:
		left:				0
		top:				0
		width:				{ stylesheet.card_width }
		height:				{ stylesheet.card_height }
		z index:			420
		visible:			{ is_crowned() }
		render style:		image
		image:				{ card_crown() }
	crown border:
		left:				0
		top:				0
		width:				{ stylesheet.card_width }
		height:				{ stylesheet.card_height }
		z index:			400
		visible:			{ is_crowned() }
		mask:				border_crown_mask.png
	loyalty box:
		left:				0
		top:				0
		width:				{ stylesheet.card_width }
		height:				{ stylesheet.card_height }
		z index:			810
		visible:			{ card.loyalty != "" }
		render style:		image
		image:				loyalty/loy.png
	loyalty cost box 1:
		left:				0
		top:				{ ability_center(1) - 39 + loyal_move(1) }
		width:				77
		height:				77
		z index:			810
		visible:			{ card.loyalty_cost_1 != "" and abilities_count() >= 1 }
		render style:		image
		image:				{ card_loyaltycostbox(1) }
	loyalty cost box 2:
		left:				0
		top:				{ ability_center(2) - 39 + loyal_move(2) }
		width:				77
		height:				77
		z index:			811
		visible:			{ card.loyalty_cost_2 != "" and abilities_count() >= 2 }
		render style:		image
		image:				{ card_loyaltycostbox(2) }
	loyalty cost box 3:
		left:				0
		top:				{ ability_center(3) - 39 + loyal_move(3) }
		width:				77
		height:				77
		z index:			812
		visible:			{ card.loyalty_cost_3 != "" and abilities_count() >= 3}
		render style:		image
		image:				{ card_loyaltycostbox(3) }
	loyalty cost box 4:
		left:				0
		top:				{ ability_center(4) - 39 + loyal_move(4) }
		width:				77
		height:				77
		z index:			813
		visible:			{ card.loyalty_cost_4 != "" and abilities_count() >= 4 }
		render style:		image
		image:				{ card_loyaltycostbox(4) }
	loyalty cost box 5:
		left:				0
		top:				{ ability_center(5) - 39 + loyal_move(5) }
		width:				77
		height:				77
		z index:			814
		visible:			{ card.loyalty_cost_5 != "" and abilities_count() >= 5 }
		render style:		image
		image:				{ card_loyaltycostbox(5) }
	loyalty cost box 6:
		left:				0
		top:				{ ability_center(6) - 39 + loyal_move(6) }
		width:				77
		height:				77
		z index:			815
		visible:			{ card.loyalty_cost_6 != "" and abilities_count() >= 6 }
		render style:		image
		image:				{ card_loyaltycostbox(6) }
	colon 1:
		left:				60
		top:				{ ability_center(1) - 13 + loyal_move(1) }
		width:				5
		height:				22
		z index:			1010
		visible:			{ card.loyalty_cost_1 != "" and abilities_count() >= 1 }
		font:
			name:				Beleren Bold
			size:				13
			color:				{ styling.rule_text_color }
		alignment: center middle
	colon 2:
		left:				60
		top:				{ ability_center(2) - 13 + loyal_move(2) }
		width:				5
		height:				22
		z index:			1011
		visible:			{ card.loyalty_cost_2 != "" and abilities_count() >= 2 }
		font:
			name:				Beleren Bold
			size:				13
			color:				{ styling.rule_text_color }
		alignment: center middle
	colon 3:
		left:				60
		top:				{ ability_center(3) - 13 + loyal_move(3) }
		width:				5
		height:				22
		z index:			1012
		visible:			{ card.loyalty_cost_3 != "" and abilities_count() >= 3 }
		font:
			name:				Beleren Bold
			size:				13
			color:				{ styling.rule_text_color }
		alignment: center middle
	colon 4:
		left:				60
		top:				{ ability_center(4) - 13 + loyal_move(4) }
		width:				5
		height:				22
		z index:			1013
		visible:			{ card.loyalty_cost_4 != "" and abilities_count() >= 4 }
		font:
			name:				Beleren Bold
			size:				13
			color:				{ styling.rule_text_color }
		alignment: center middle
	colon 5:
		left:				60
		top:				{ ability_center(5) - 13 + loyal_move(5) }
		width:				5
		height:				22
		z index:			1014
		visible:			{ card.loyalty_cost_5 != "" and abilities_count() >= 5 }
		font:
			name:				Beleren Bold
			size:				13
			color:				{ styling.rule_text_color }
		alignment: center middle
	colon 6:
		left:				60
		top:				{ ability_center(6) - 13 + loyal_move(6) }
		width:				5
		height:				22
		z index:			1015
		visible:			{ card.loyalty_cost_6 != "" and abilities_count() >= 6 }
		font:
			name:				Beleren Bold
			size:				13
			color:				{ styling.rule_text_color }
		alignment: center middle
	textbox stripe 1:
		left:				0
		width:				375
		top:				{ textbox_background_top() }
		bottom:				{ stripe_top(1) }
		z index:			510
		visible:			{ abilities_count() >= 2 }
		render style:		image
		image:				{ set_alpha(recolor_image("textbox_background.png", color: rgb(255,255,255)), alpha: if styling.textbox_color == rgb(255,255,254) then 0.35 else 0.55) }
		mask:				{ top := textbox_background_top(); crop_safe("textbox_background_mask.png", offset_x: 0, offset_y: top, width: 375, height: stripe_top(1) - top) }
	textbox stripe 2:
		left:				0
		width:				375
		top:				{ stripe_top(2) + stripe_height() }
		bottom:				{ if abilities_count() >= 4 then stripe_top(3) else textbox_background_bottom() }
		z index:			510
		visible:			{ abilities_count() >= 3 }
		render style:		image
		image:				{ set_alpha(recolor_image("textbox_background.png", color: rgb(255,255,255)), alpha: if styling.textbox_color == rgb(255,255,254) then 0.35 else 0.55) }
		mask:				{ top := stripe_top(2) + stripe_height(); crop_safe("textbox_background_mask.png", offset_x: 0, offset_y: top, width: 375, height: (if abilities_count() >= 4 then stripe_top(3) else textbox_background_bottom()) - top) }
	textbox stripe 3:
		left:				0
		width:				375
		top:				{ stripe_top(4) + stripe_height() }
		bottom:				{ if abilities_count() >= 6 then stripe_top(5) else textbox_background_bottom() }
		z index:			510
		visible:			{ abilities_count() >= 5 }
		render style:		image
		image:				{ set_alpha(recolor_image("textbox_background.png", color: rgb(255,255,255)), alpha: if styling.textbox_color == rgb(255,255,254) then 0.35 else 0.55) }
		mask:				{ top := stripe_top(4) + stripe_height(); crop_safe("textbox_background_mask.png", offset_x: 0, offset_y: top, width: 375, height: (if abilities_count() >= 6 then stripe_top(5) else textbox_background_bottom()) - top) }
	textbox stripe separator 1:
		left:				0
		width:				375
		top:				{ stripe_top(1) }
		height:				{ stripe_height() }
		z index:			510
		visible:			{ abilities_count() >= 2 }
		render style:		image
		image:				{ set_alpha(recolor_image("stripe_bottom.png", color: rgb(255,255,255)), alpha: if styling.textbox_color == rgb(255,255,254) then 0.35 else 0.55) }
	textbox stripe separator 2:
		left:				0
		width:				375
		top:				{ stripe_top(2) }
		height:				{ stripe_height() }
		z index:			510
		visible:			{ abilities_count() >= 3 }
		render style:		image
		image:				{ set_alpha(recolor_image("stripe_top.png", color: rgb(255,255,255)), alpha: if styling.textbox_color == rgb(255,255,254) then 0.35 else 0.55) }
	textbox stripe separator 3:
		left:				0
		width:				375
		top:				{ stripe_top(3) }
		height:				{ stripe_height() }
		z index:			510
		visible:			{ abilities_count() >= 4 }
		render style:		image
		image:				{ set_alpha(recolor_image("stripe_bottom.png", color: rgb(255,255,255)), alpha: if styling.textbox_color == rgb(255,255,254) then 0.35 else 0.55) }
	textbox stripe separator 4:
		left:				0
		width:				375
		top:				{ stripe_top(4) }
		height:				{ stripe_height() }
		z index:			510
		visible:			{ abilities_count() >= 5 }
		render style:		image
		image:				{ set_alpha(recolor_image("stripe_top.png", color: rgb(255,255,255)), alpha: if styling.textbox_color == rgb(255,255,254) then 0.35 else 0.55) }
	textbox stripe separator 5:
		left:				0
		width:				375
		top:				{ stripe_top(5) }
		height:				{ stripe_height() }
		z index:			510
		visible:			{ abilities_count() >= 6 }
		render style:		image
		image:				{ set_alpha(recolor_image("stripe_bottom.png", color: rgb(255,255,255)), alpha: if styling.textbox_color == rgb(255,255,254) then 0.35 else 0.55) }
	textbox background:
		left:				0
		width:				375
		top:				{ textbox_background_top() }
		bottom:				{ textbox_background_bottom() }
		z index:			500
		render style:		image
		image:				{ textbox_image() }
		mask:				{ top := textbox_background_top(); crop_safe("textbox_background_mask.png", offset_x: 0, offset_y: top, width: 375, height: textbox_background_bottom() - top) }
	card stamp rim:
		left:				0
		top:				0
		width:				{ stylesheet.card_width }
		height:				{ stylesheet.card_height }
		z index:			710
		render style:		image
		image:				{ if is_stamped() then card_stamprim() else card_nonstamprim() }
		mask:				stamp_rim_mask.png
	card stamp border:
		left:				0
		top:				0
		width:				{ stylesheet.card_width }
		height:				{ stylesheet.card_height }
		z index:			700
		visible:			{ is_stamped() }
		mask:				border_stamp_mask.png
	foil layer:
		left:				0
		top :				0
		width:				{ stylesheet.card_width }
		height:				{ stylesheet.card_height }
		z index:			3000
		visible:			{ styling.overlay != "none" and styling.overlay != "" }
		render style:		image
		image:				{ if styling.overlay == "" then "" else styling.overlay + "/overlay.png" }
		mask:				{ "foil_mask" + (if is_stamped() then "_rare" else "") + (if card.loyalty != "" then "_pt" else "") + (if is_crowned() then "_crown" else "") + ".png"}
	corners:
		left:				0
		top :				0
		width:				{ stylesheet.card_width }
		height:				{ stylesheet.card_height }
		z index:			4000
		visible:			{ styling.corners != "none" }
		mask:				{ if styling.corners == "rounded and antialiased" then "corners_rounded_antialiased_mask.png" else "corners_rounded_mask.png" }
