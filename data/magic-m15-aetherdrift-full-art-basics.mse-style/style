mse version:		2.1.2
game:				magic
short name:			DFT Basics
full name:			Aetherdrift Full Art
icon:				card_sample.png
position hint:		099

version:			2024-05-20
depends on:
	package:			magic.mse-game
	version:			2014-06-25
depends on:
	package:			magic-mana-large.mse-symbol-font
	version:			2007-09-23
depends on:
	package:			magic-mana-small.mse-symbol-font
	version:			2007-09-23
depends on:
	package:			magic-modules.mse-include
	version:			2024-10-01
depends on:
	package:			magic-mainframe-extras.mse-include
	version:			2007-09-23

card width:			744
card height:		1039
card dpi:			297

### blanks by GenevensiS
### code by GenevensiS

############################################################## Extra scripts

init script:
	swap_fonts_type_default :=
	[
		name: {"Beleren Bold"},
		size: {26.5},
		color: { "white" },
		vertical: {0},
		italic: {""}
	]
	
	typeline_offset_top_1 := { 294 }
	typeline_offset_left_1 := { 77 }
	typeline_offset_width_1 := { -77 }
	transform_symbol_disabled_1 := { true }
	card_symbol_disabled_1 := { true }
	swap_fonts_name_default :=
	[
		name: {"Beleren Bold"},
		size: {30.0},
		color: { "white" },
		vertical: {0},
		italic: {""}
	]
	
	two_types := { styling.second_land_type != "none" }
	
	card_symbol :=
	{
		first := "symbol/" + type_to_letter_map[styling.land_type] + ".png"
		if not two_types() then first else (
		second := "symbol/" + type_to_letter_map[styling.second_land_type] + ".png"
		linear_blend(
			image1: first,
			image2: second,
			x1: 0.49,  y1: 0,
			x2: 0.51,  y2: 0
		))
	}
	
	card_full_name := { value }
	card_full_name_default := { type_to_name_map[styling.land_type + "," + styling.second_land_type] }
	name_default := { type_to_name_map[styling.land_type + "," + styling.second_land_type] }
	
	super_type_filter_default := { (if two_types() then "" else "Basic ") + "Land" }
	sub_type_filter_default :=
	{
		base := to_title(styling.land_type) + (if two_types() then " " + to_title(styling.second_land_type) else "")
		if base == "Wastes" then "" else base
	}
	
	border_color_default := { rgb(246,193,19) }
	
	type_to_name_map :=
	[
		"plains,none":			"Plains",
		"island,none":			"Island",
		"swamp,none":			"Swamp",
		"mountain,none":		"Mountain",
		"forest,none":			"Forest",
		"wastes,none":			"Wastes",
		
		"plains,wastes":		"Wasted Plains",
		"island,wastes":		"Wasted Island",
		"swamp,wastes":			"Wasted Swamp",
		"mountain,wastes":		"Wasted Mountain",
		"forest,wastes":		"Wasted Forest",
		"wastes,wastes":		"Wastes",
		
		"plains,plains":		"Plains",
		"island,plains":		"Tundra",
		"swamp,plains":			"Scrubland",
		"mountain,plains":		"Plateau",
		"forest,plains":		"Savannah",
		"wastes,plains":		"Wasted Plains",
		
		"plains,island":		"Tundra",
		"island,island":		"Island",
		"swamp,island":			"Underground Sea",
		"mountain,island":		"Volcanic Island",
		"forest,island":		"Tropical Island",
		"wastes,island":		"Wasted Island",
		
		"plains,swamp":			"Scrubland",
		"island,swamp":			"Underground Sea",
		"swamp,swamp":			"Swamp",
		"mountain,swamp":		"Badlands",
		"forest,swamp":			"Bayou",
		"wastes,swamp":			"Wasted Swamp",
		
		"plains,mountain":		"Plateau",
		"island,mountain":		"Volcanic Island",
		"swamp,mountain":		"Badlands",
		"mountain,mountain":	"Mountain",
		"forest,mountain":		"Taiga",
		"wastes,mountain":		"Wasted Mountain",
		
		"plains,forest":		"Savannah",
		"island,forest":		"Tropical Island",
		"swamp,forest":			"Bayou",
		"mountain,forest":		"Taiga",
		"forest,forest":		"Forest",
		"wastes,forest":		"Wasted Forest"
	]
	type_to_letter_map :=
	[
		plains:		"w",
		island:		"u",
		swamp:		"b",
		mountain:	"r",
		forest:		"g",
		wastes:		"c",
		none:		"none"
	]
	type_to_color_map :=
	[
		plains:		", white",
		island:		", blue",
		swamp:		", black",
		mountain:	", red",
		forest:		", green",
		wastes:		"",
		none:		", none"
	]
	
	is_promo := { styling.promo }
	
	is_unsorted := { styling.remove_from_autocount }
	
	popout_left :=		{ split := split_comma(styling.popout_art_coordinates); if length(split) > 3 and split.0 != "" then clamp(split.0, minimum: -500,	maximum: 800) else 0 }
	popout_top :=		{ split := split_comma(styling.popout_art_coordinates); if length(split) > 3 and split.1 != "" then clamp(split.1, minimum: -500,	maximum: 1100) else 0 }
	popout_width :=		{ split := split_comma(styling.popout_art_coordinates); if length(split) > 3 and split.2 != "" then clamp(split.2, minimum: 0,		maximum: 1500) else 0 }
	popout_height :=	{ split := split_comma(styling.popout_art_coordinates); if length(split) > 3 and split.3 != "" then clamp(split.3, minimum: 0,		maximum: 2000) else 0 }
	
	art_left :=			{ if styling.stretch_art_to_whole_card then 0							else if styling.border != "yes" then 0						else 30 }
	art_top :=			{ if styling.stretch_art_to_whole_card then 0							else if styling.border != "yes" then 0						else 30 }
	art_width :=		{ if styling.stretch_art_to_whole_card then stylesheet.card_width		else if styling.border != "yes" then stylesheet.card_width	else 684 }
	art_bottom :=		{ if styling.stretch_art_to_whole_card then stylesheet.card_height + 1	else if styling.border != "no"	then 954					else stylesheet.card_height + 1 }
	art_mask :=			{ crop("image_mask.png", offset_x: art_left(), offset_y: art_top(), width: art_width(), height: art_bottom() - art_top()) }

############################################################## Extra style options

styling field:
	type:			choice
	name:			land type
	description:	What type is this card?
	choice:			plains
	choice:			island
	choice:			swamp
	choice:			mountain
	choice:			forest
	choice:			wastes
	initial:		plains
styling field:
	type:			choice
	name:			second land type
	description:	What type is this card?
	choice:
		name:			none
		line below:		true
	choice:			plains
	choice:			island
	choice:			swamp
	choice:			mountain
	choice:			forest
	choice:			wastes
	initial:		none
styling field:
	type:			text
	name:			popout art coordinates
	description:	The coordinates for the popout image field. Formatted as "left,top,width,height". Used to make the illustration jump in front of the frame.
styling field:
	type:			boolean
	name:			stretch art to whole card
	description:	Make the image span the whole card instead of just the visible part? Use this to help make popout effects.
	initial:		no
styling field:
	type:			package choice
	name:			overlay
	description:	Should there be an overlay applied, such as foil?
	match:			magic-overlay-*.mse-include
	required:		false
styling field:
	type:			choice
	name:			border
	description:	Use a border?
	choice:			yes
	choice:			only copyright
	choice:			no
	initial:		yes
styling field:
	type:			boolean
	name:			remove from autocount
	description:	Removes the automatic card number for specific cards, to allow for overcounted cards like in Planeswalker Decks.
	initial:		no
styling field:
	type:			boolean
	name:			promo
	description:	Is this card a promo card, with the "P" rarity?
	initial:		no
styling field:
	type:			color
	name:			copyright text color
	description:	Color of the copyright, illustrator, card number, and set code text.
	choice:			white
	choice:			black
	initial:		black

############################################################## Card fields

include file: /magic-modules.mse-include/information/card_fields

include file: /magic-modules.mse-include/stamps/card_fields

include file: /magic-modules.mse-include/corners/card_fields

include file: /magic-modules.mse-include/namelines/card_fields

include file: /magic-modules.mse-include/typelines/card_fields

card style:
	############################# Background stuff
	border color:
		left:				0
		top:				0
		width:				{ stylesheet.card_width }
		height:				{ stylesheet.card_height }
		z index:			100
		visible:			{ styling.border != "no" }
		mask:				{ "/magic-modules.mse-include/borders/744x1039/m15/full_art/" + (if styling.border == "yes" then "base.png" else "borderless.png") }
	############################# Image
	image:
		left:				{ art_left() }
		width:				{ art_width() }
		top:				{ art_top() }
		bottom:				{ art_bottom() }
		z index:			0
		mask:				{ art_mask() }
	mainframe image:
		left:				{ popout_left() }
		top:				{ popout_top() }
		width:				{ popout_width() }
		height:				{ popout_height() }
		z index:			1010

############################################################## Extra card fields

extra card field:
	type:				color
	name:				card stamp border
	script:				card.border_color
	editable:			false
	save value:			false
	show statistics:	false
extra card field:
	type:				choice
	name:				symbol
	editable:			false
	save value:			false
	show statistics:	false
	choice:				symbol
extra card field:
	type:				choice
	name:				card background
	editable:			false
	save value:			false
	show statistics:	false
	choice:				card background
extra card field:
	type:				choice
	name:				foil layer
	editable:			false
	save value:			false
	show statistics:	false
	choice:				foil layer

extra card style:
	card stamp border:
		left:				0
		top:				0
		width:				{ stylesheet.card_width }
		height:				{ stylesheet.card_height }
		z index:			110
		visible:			{ styling.border != "no" and is_stamped() }
		mask:				{ if card.card_stamp == "universes beyond" then "mask_stamp_background_border_triangle.png" else "mask_stamp_background_border.png" }
	card background:
		left:				0
		top:				0
		width:				744
		height:				1039
		z index:			200
		render style:		image
		image:				card.png
	symbol:
		left:				32
		top:				868
		width:				81
		height:				81
		z index:			210
		render style:		image
		image:				{ card_symbol() }
	foil layer:
		left:				0
		top:				0
		width:				{ stylesheet.card_width }
		height:				{ stylesheet.card_height }
		z index:			1050
		visible:			{ styling.overlay != "none" and styling.overlay != "" }
		render style:		image
		image:				{ if styling.overlay == "" then "" else styling.overlay + "/overlay.png" }
