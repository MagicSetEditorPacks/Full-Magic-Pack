mse version: 2.0.0
game: magic
short name: Booster Pack
full name: Pack
icon: card-sample.png
installer group: magic/m15 style/normal cards
position hint: 999

version: 2021-03-09
depends on:
	package: magic.mse-game
	version: 2014-06-25

card width: 600
card height: 1060
card dpi: 150
############################################################## Extra scripts
init script:
	is_unsorted := {true}
	logo_deets := {split_text(styling.logo_placement+",,,,,", match:",")}
	title_deets := {split_text(styling.main_title_placement+",,,,,", match:",")}
	subtitle_deets := {split_text(styling.sub_title_placement+",,,,,", match:",")}
	count_deets := {split_text(styling.card_count_placement+",,,,,", match:",")}
	age_deets := {split_text(styling.age_placement+",,,,,", match:",")}
	artist_deets := {split_text(styling.artist_placement+",,,,,", match:",")}
	rarity_deets := {split_text(styling.rarity_placement+",,,,,", match:",")}
	color_fringe := {styling.fringe_color != rgb(0,0,1)}
	texture_mode := {
		if styling.text_texture_mode == "on" then "on"
		else if styling.text_texture_mode == "masking" then "masking"
		else if styling.text_texture_mode == "import" then "import"
		else if styling.text_texture_mode == "texture" then "texture"
		else "off"		
	}
	font_sizes := {split_text(styling.font_sizes+",,,,", match:",")}
	title_font_size := {if font_sizes().0 == "" then 70 else font_sizes().0}
	subtitle_font_size := {if font_sizes().1 == "" then 40 else font_sizes().1}
	count_font_size := {if font_sizes().2 == "" then 30 else font_sizes().2}
	age_font_size := {if font_sizes().3 == "" then 20 else font_sizes().3}
	font_color := {if texture_mode() == "masking" then "white" else styling.title_color}
	arrow_coords := {
		angle := if artist_deets().4 != "" then artist_deets().4 else 0
		angle := (to_number(angle)+10.126)*3.14159265/180
		left := if artist_deets().0 != "" then artist_deets().0 else 39
		top := if artist_deets().1 != "" then artist_deets().1 else 910
		left := left - 28.44*cos(angle)
		top := top - (-28.44*sin(angle))
		[left, top]
	}
	use_main_rarity := { contains(set.mainframe_rarity_name, match: ".png")}
	un_png := replace@(match:".png", replace: "")
	rarity_alpha := {to_number(styling.rarity_opacity)/100}
############################################################## Set info fields
set info style:
	symbol:
		variation:
			name: invertedcommon
			border radius: 0.10
			fill type: solid
			fill color:   rgb(255,255,255)
			border color: rgb(0,0,0)
############################################################## Extra style options
styling field:
	type: color
	name: fringe color
	choice:
		name: none
		color: rgb(0,0,1)
	choice:
		name: black
		color: rgb(0,0,0)
	choice:
		name: white
		color: rgb(255,255,255)
	choice:
		name: silver
		color: rgb(200,200,200)
	choice:
		name: gold
		color: rgb(200,180,0)
	description: The font color for the pack
styling field:
	type: color
	name: title color
	choice:
		name: black
		color: rgb(0,0,0)
	choice:
		name: white
		color: rgb(255,255,255)
	choice:
		name: silver
		color: rgb(200,200,200)
	choice:
		name: gold
		color: rgb(200,180,0)
	default: "black"
	description: The font color for the title
styling field:
	type: color
	name: card count color
	choice:
		name: black
		color: rgb(0,0,0)
	choice:
		name: white
		color: rgb(255,255,255)
	choice:
		name: silver
		color: rgb(200,200,200)
	choice:
		name: gold
		color: rgb(200,180,0)
	default: "black"
	description: The font color for the card count text
styling field:
	type: color
	name: age color
	choice:
		name: black
		color: rgb(0,0,0)
	choice:
		name: white
		color: rgb(255,255,255)
	choice:
		name: silver
		color: rgb(200,200,200)
	choice:
		name: gold
		color: rgb(200,180,0)
	default: "black"
	description: The font color for the age text
styling field:
	type: color
	name: artist color
	choice:
		name: black
		color: rgb(0,0,0)
	choice:
		name: white
		color: rgb(255,255,255)
	choice:
		name: silver
		color: rgb(200,200,200)
	choice:
		name: gold
		color: rgb(200,180,0)
	default: "black"
	description: The font color for the age text
styling field:
	type: boolean
	name: small text over title
	description: add a small textbox over the big title. set false to move it under
styling field:
	type: choice
	name: text texture mode
	choice: off
	choice: masking
	choice: import
	choice: texture
	choice: on
	choice: switch to masking to generate mask
	choice: save card image, can grow selection in image editors for borders
	choice: switch to import and import saved mask
	choice: switch to texture and import texture image
	choice: switch to on to apply texture
styling field:
	type: text
	name: font sizes
	description: Formatted "1,2,3,4". Change the font size of big title, small title, card count, and age. Default is "70,40,30,20"
styling field:
	type: text
	name: rarity opacity
	description: Opacity of rarity symbol, 0 is fully transparent, 100 is fully opaque. Default is 0.
styling field:
	type: text
	name: logo placement
	description: Formatted "left,top,width,height,rotation," default is "44,100,516,175,0,"
styling field:
	type: text
	name: main title placement
	description: Formatted "left,top,width,height,rotation," default is "15,750,570,100,0,"
styling field:
	type: text
	name: sub title placement
	description: Formatted "left,top,width,height,rotation," default is "15,700,570,50,0,"
styling field:
	type: text
	name: card count placement
	description: Formatted "left,top,width,height,rotation," default is "44,940,570,50,0,"
styling field:
	type: text
	name: age placement
	description: Formatted "left,top,width,height,rotation," default is "480,123,100,30,0,"
styling field:
	type: text
	name: artist placement
	description: Formatted "left,top,width,height,rotation," default is "39,910,200,20,0,"
styling field:
	type: text
	name: rarity placement
	description: Formatted "left,top,width,height,rotation," default is "150,630,300,300,0,"
styling field:
	type: boolean
	name: inverted common symbol
	description: Should the common rarity symbol be inverted, like in Coldsnap?
	initial: no
############################################################## Card fields
card style:
	############################# Name line
	name:
		left: {if title_deets().0 != "" then title_deets().0 else 15}
		top: {if title_deets().1 != "" then title_deets().1 else if styling.small_text_over_title then 750 else 700 }
		width: {if title_deets().2 != "" then title_deets().2 else 570}
		height: {if title_deets().3 != "" then title_deets().3 else 100}
		angle: {if title_deets().4 != "" then title_deets().4 else 0}
		alignment: center shrink-overflow
		padding bottom: 0
		z index: 7
		font:
			name: Beleren Bold
			size: {title_font_size()}
			color: {font_color()}
	############################# Image
	image:
		left: {if color_fringe() then 11 else 0}
		top: {if color_fringe() then 125 else 0}
		width: {if color_fringe() then 578 else 600}
		height:	{if color_fringe() then 812 else 1060}
		z index: 1
		mask: {if color_fringe() then "" else "booster_mask.png"}
	mainframe image:
		left: 0
		top: 0
		width: {if texture_mode() == "import" then 600 else 0}
		height: 1060
		z index: 8
	image 2:
		left: 0
		top: 0
		width: {if texture_mode() == "texture" or texture_mode() == "on" then 600 else 0}
		height: 1060
		z index: 8
		mask: {if texture_mode() == "on" then card.mainframe_image else ""}
	############################# Set symbol
	rarity:
		left: {if rarity_deets().0 != "" then rarity_deets().0 else 150}
		top: {if rarity_deets().1 != "" then rarity_deets().1 else 630}
		width: {if rarity_deets().2 != "" then rarity_deets().2 else 300}
		height: {if rarity_deets().3 != "" then rarity_deets().3 else 300}
		angle: {if rarity_deets().4 != "" then rarity_deets().4 else 0}
		z index: 3
		render style: image
		alignment: middle right
		choice images:
			# Images based on the set symbol
			basic land:
				script:
					set_alpha(alpha: rarity_alpha(), 
						if use_main_rarity() then "/magic-mainframe-extras.mse-include/" + un_png(set.mainframe_rarity_name) + "c.png"
						else if styling.inverted_common_symbol then symbol_variation(symbol: set.symbol, variation: "invertedcommon")
						else symbol_variation(symbol: set.symbol, variation: "common")
					)
			common:
				script:
					set_alpha(alpha: rarity_alpha(), 
						if use_main_rarity() then "/magic-mainframe-extras.mse-include/" + un_png(set.mainframe_rarity_name) + "c.png"
						else if styling.inverted_common_symbol then symbol_variation(symbol: set.symbol, variation: "invertedcommon")
						else symbol_variation(symbol: set.symbol, variation: "common")
					)
			uncommon:
				script:
					set_alpha(alpha: rarity_alpha(), 
						if use_main_rarity() then "/magic-mainframe-extras.mse-include/" + un_png(set.mainframe_rarity_name) + "u.png"
						else symbol_variation(symbol: set.symbol, variation: "uncommon")
					)
			rare:
				script:
					set_alpha(alpha: rarity_alpha(), 
						if use_main_rarity() then "/magic-mainframe-extras.mse-include/" + un_png(set.mainframe_rarity_name) + "r.png"
						else symbol_variation(symbol: set.symbol, variation: "rare")
					)
			mythic rare:
				script:
					set_alpha(alpha: rarity_alpha(), 
						if use_main_rarity() then "/magic-mainframe-extras.mse-include/" + un_png(set.mainframe_rarity_name) + "m.png"
						else symbol_variation(symbol: set.symbol, variation: "mythic rare")
					)
			special:
				script:
					set_alpha(alpha: rarity_alpha(), 
						if use_main_rarity() then "/magic-mainframe-extras.mse-include/" + un_png(set.mainframe_rarity_name) + "s.png"
						else symbol_variation(symbol: set.symbol, variation: "special")
					)
			masterpiece:
				script:
					set_alpha(alpha: rarity_alpha(), 
						if use_main_rarity() then "/magic-mainframe-extras.mse-include/" + un_png(set.mainframe_rarity_name) + "mp.png"
						else symbol_variation(symbol: set.masterpiece_symbol, variation: "mythic rare")
					)

	############################# Copyright stuff
	illustrator:
		left: {if artist_deets().0 != "" then artist_deets().0 else 41}
		top: {if artist_deets().1 != "" then artist_deets().1 else 910}
		width: {if artist_deets().2 != "" then artist_deets().2 else 200}
		height: {if artist_deets().3 != "" then artist_deets().3 else 20}
		angle: {if artist_deets().4 != "" then artist_deets().4 else 0}
		z index: 1
		font:
			name: Beleren Small Caps Bold
			size: 15
			color: {styling.artist_color}
############################################################## Extra card fields
extra card field:
	type: choice
	name: artist arrow
	choice: light
	choice: dark
extra card field:
	type: choice
	name: back drop
	choice: back
	editable: false
	save value: false
extra card field:
	type: color
	name: fake back drop
	script: rgb(0,0,0)
	editable: false
	save value: false
extra card field:
	type: color
	name: fringes
	script: styling.fringe_color
	editable: false
	save value: false
extra card field:
	type: choice
	name: magic logo
	choice: light
	choice: dark
	choice: none
extra card field:
	type: text
	name: small name
extra card field:
	type: text
	name: age
	default: "AGE 13+"
extra card field:
	type: text
	name: count
	default: "15-CARD BOOSTER PACK"
extra card style:
	back drop:
		left: 0
		top: 0
		width: 600
		height: 1060
		z index: -1
		image: back_drop.png
		render style: image
	fake back drop:
		left: 0
		top: 0
		width: 600
		height: 1060
		z index: 6
		visible: {texture_mode() == "masking"}
	fringes:
		left: 0
		top: 0
		width: 600
		height: 1060
		z index: 2
		mask: fringe_mask.png
		visible: {color_fringe()}
	artist arrow:
		left: {arrow_coords().0}
		top: {arrow_coords().1}
		angle: {if artist_deets().4 != "" then artist_deets().4 else 0}
		width: 24
		height: 14
		z index: 1
		render style: image
		visible: {card.illustrator != ""}
		choice images:
			light: artist_arrow_light.png
			dark: artist_arrow_dark.png
	magic logo:
		left: {if logo_deets().0 == "" then 44 else logo_deets().0}
		top: {if logo_deets().1 == "" then 100 else logo_deets().1}
		width: {if logo_deets().2 == "" then 516 else logo_deets().2}
		height: {if logo_deets().3 == "" then 175 else logo_deets().3}
		angle: {if logo_deets().4 == "" then 0 else logo_deets().4}
		z index: 2
		render style: image
		popup style: in place
		choice images:
			light: magic_light.png
			dark: magic_dark.png
	small name:
		left: {if subtitle_deets().0 != "" then subtitle_deets().0 else 15}
		top: {if subtitle_deets().1 != "" then subtitle_deets().1 else if styling.small_text_over_title then 700 else 800 }
		width: {if subtitle_deets().2 != "" then subtitle_deets().2 else 570}
		height: {if subtitle_deets().3 != "" then subtitle_deets().3 else 50}
		angle: {if subtitle_deets().4 != "" then subtitle_deets().4 else 0}
		alignment: center shrink-overflow
		padding bottom: 0
		z index: 7
		font:
			name: Beleren Small Caps Bold
			size: {subtitle_font_size()}
			color: {font_color()}
	count:
		left: {if count_deets().0 != "" then count_deets().0 else 15}
		top: {if count_deets().1 != "" then count_deets().1 else 940}
		width: {if count_deets().2 != "" then count_deets().2 else 570}
		height: {if count_deets().3 != "" then count_deets().3 else 50}
		angle: {if count_deets().4 != "" then count_deets().4 else 0}
		alignment: center shrink-overflow
		padding bottom: 0
		z index: 4
		font:
			name: Beleren Small Caps Bold
			size: {count_font_size()}
			color: {styling.card_count_color}
	age:
		left: {if age_deets().0 != "" then age_deets().0 else 480}
		top: {if age_deets().1 != "" then age_deets().1 else 123}
		width: {if age_deets().2 != "" then age_deets().2 else 100}
		height: {if age_deets().3 != "" then age_deets().3 else 30}
		angle: {if age_deets().4 != "" then age_deets().4 else 0}
		alignment: center shrink-overflow
		padding bottom: 0
		z index: 4
		font:
			name: Beleren Small Caps Bold
			size: {age_font_size()}
			color: {styling.age_color}
