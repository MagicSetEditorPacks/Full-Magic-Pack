mse version:		2.1.2
game:				magic
short name:			Compleated
full name:			Planeswalkers
icon:				card_sample.png
position hint:		017

version:			2024-05-20
depends on:
	package:			magic.mse-game
	version:			2014-06-25
depends on:
	package:			magic-mana-large.mse-symbol-font
	version:			2007-09-23
depends on:
	package:			magic-mana-small.mse-symbol-font
	version:			2007-09-23
depends on:
	package:			magic-modules.mse-include
	version:			2024-05-20
depends on:
	package:			magic-mainframe-extras.mse-include
	version:			2007-09-23

card width:			750
card height:		1046
card dpi:			300

############################################################## Extra scripts
init script:
	template_prefix := [
		card: "card/",
		stamp: "stamp/",
		identity: "/magic-modules.mse-include/indicators/"
	]
	template_suffix := [
		card: "card.png",
		stamp: "stamp.png",
		identity: "identity.png"
	]
	template := { template_prefix[type] + input + template_suffix[type] }
	land_template := { template_prefix[type] + input + template_suffix[type] }

	is_promo :=			{ styling.promo }
	is_unsorted :=		{ styling.remove_from_autocount }

	include file: /magic-modules.mse-include/loyalty/init_script
	loyalty_textbox_mask := {"loyalty_mask.png"}

	swap_fonts_body_default := [
		name: {"MPlantin"},
		italic: {"MPlantin-Italic"},
		color: {"black"},
		vertical: {0},
		size: {28}
	]
	swap_fonts_name_default := [
		name: {"Beleren Bold"},
		italic: {""},
		color: {"black"},
		vertical: {0},
		size: {30}
	]
	swap_fonts_type_default := [
		name: {"Beleren Bold"},
		italic: {""},
		color: {"black"},
		vertical: {0},
		size: {26}
	]

	foil_mask_added_sections := {
		output := []
		if card.loyalty != "" then output := output + ["foil_mask_loyalty.png"]
		output
	}@(face:1)
		
	foil_mask_removed_sections := {
		output := []
		if is_stamped() then output := output + ["foil_mask_round.png"]
		output
	}@(face:1)

	typeline_offset_top_1 := {-72}
	nameline_offset_top_1 := {-16}
############################################################## Extra style options
include file: /magic-modules.mse-include/popouts/styling_fields
include file: /magic-modules.mse-include/texts/styling_fields
include file: /magic-modules.mse-include/loyalty/styling_fields
include file: /magic-modules.mse-include/rarities/styling_fields
include file: /magic-modules.mse-include/watermarks/styling_fields
include file: /magic-modules.mse-include/information/styling_fields
include file: /magic-modules.mse-include/foils/styling_fields
include file: /magic-modules.mse-include/symbol-fonts/styling_fields
include file: /magic-modules.mse-include/fonts/styling_fields
############################################################## Card fields
include file: /magic-modules.mse-include/namelines/card_fields
include file: /magic-modules.mse-include/popouts/card_fields
include file: /magic-modules.mse-include/typelines/card_fields
include file: /magic-modules.mse-include/watermarks/card_fields
include file: /magic-modules.mse-include/stamps/card_fields
include file: /magic-modules.mse-include/information/card_fields
include file: /magic-modules.mse-include/foils/card_fields
include file: /magic-modules.mse-include/loyalty/card_fields
include file: /magic-modules.mse-include/corners/card_fields
card style:
	############################# Background stuff
	border color:
		left:				0
		top:				0
		width:				{ stylesheet.card_width }
		height:				{ stylesheet.card_height }
		z index:			500
		mask:				border_mask.png
	card color:
		left:				0
		top:				0
		width:				{ stylesheet.card_width }
		height:				{ stylesheet.card_height }
		z index:			200
		popup style:		in place
		render style:		image
		image:				{ module_card() }
	############################# Image
	image:
		left:				{ if styling.stretch_art_to_whole_card then 0						else 56 }
		top:				{ if styling.stretch_art_to_whole_card then 0						else 103 }
		width:				{ if styling.stretch_art_to_whole_card then stylesheet.card_width	else 644 }
		height:				{ if styling.stretch_art_to_whole_card then stylesheet.card_height	else 856 }
		z index:			100
	############################# Text box
	text:
		left:				92
		top:				{ 586 + chop_top() + body_font_vertical() }
		right:				{ 698 - chop_right() }
		bottom:				{ 958 - chop_bottom() - if is_stamped() then 7 else 0 }
		z index:			900
		padding left: 12
		padding right: 8
		line height hard:	{ if is_modal(card.rule_text) then 0.9 else 1.2 }
		line height line:	1.5
		line height soft:	0.9
		line height hard max:	{ if is_modal(card.rule_text) then 1.0 else 1.3 }
		line height line max:	2.2
		alignment:
			script:
				if (styling.center_text == "short text only" and
				   not contains(match:"\n", card.rule_text) and
				   card.flavor_text == "<i-flavor></i-flavor>" and
				   card_style.text.content_lines <= 2) or
				   styling.center_text == "always"
				then "middle center"
				else "middle left"
		font:
			name:				{ body_font() }
			italic name:		{ body_font_italic() }
			size:				{ body_font_size() }
			color:				{ body_font_color() }
			scale down to:		12
		symbol font:
			name:				{ styling.text_box_mana_symbols }
			size:				{ body_font_size() }
			scale down to:		12
############################################################## Extra card fields
extra card field:
	type: choice
	name: stamp backing
	script: card.card_color
	editable: false
	save value: false
extra card field:
	type: color
	name: stamp border
	script: card.border_color
	editable: false
	save value: false

extra card style:
	stamp backing:
		left: 			318
		top:			920
		width:			114
		height:			70
		z index: 		840
		render style:	image
		image:			{ module_stamp() }
		visible:		{ is_stamped() }
	stamp border:
		left: 			318
		top:			920
		width:			114
		height:			70
		z index: 		840
		visible:		{ is_stamped() }
		mask:			stamp/border_mask.png
